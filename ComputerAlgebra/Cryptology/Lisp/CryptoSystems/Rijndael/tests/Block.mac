/* Matthew Gwynne, 21.1.2010 (Swansea) */
/* Copyright 2010 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/tests/Block.mac
  \brief Tests for Advanced Encryption Standard

Use by

oklib_load("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/tests/Block.mac");

*/

/*!
\htmlonly
*/

okltest_rijn_m2l(f) := (
  assert(f(diagmatrix(4,0)) = [0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0]),
  assert(f(diagmatrix(4,1)) = [1,0,0,0,0,1,0,0,0,0,1,0,0,0,0,1]),
  assert(f(matrixmap(nat2polybin,
        matrix([1,2,3,4],[5,6,7,8],[9,10,11,12],[13,14,15,16]))) =
    map(nat2polybin,[1,5,9,13,2,6,10,14,3,7,11,15,4,8,12,16])),
  true)$

okltest_rijn_l2m(f) := (
  assert(f([0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0]) = diagmatrix(4,0)),
  assert(f([1,0,0,0,0,1,0,0,0,0,1,0,0,0,0,1]) = diagmatrix(4,1)),
  assert(f(map(nat2polybin,[1,5,9,13,2,6,10,14,3,7,11,15,4,8,12,16])) = matrixmap(nat2polybin,
        matrix([1,2,3,4],[5,6,7,8],[9,10,11,12],[13,14,15,16]))),
  true)$

okltest_ss_m2l(f) := (
  assert(okltest_rijn_m2l(f)),
  true)$

okltest_ss_l2m(f) := (
  assert(okltest_rijn_l2m(buildq([f], lambda([L], f(L,4))))),
  true)$

okltest_rijn_natl2m(f) := (
  assert(f(create_list(0,i,1,16)) = diagmatrix(4,0)),
  assert(f(create_list(1,i,1,16)) = genmatrix(lambda([a,b],1),4,4)),
  assert(f([1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16]) = matrix(
      [1,x^2+1,x^3+1,x^3+x^2+1],
      [x,x^2+x,x^3+x,x^3+x^2+x],
      [x+1,x^2+x+1,x^3+x+1,x^3+x^2+x+1],
      [x^2,x^3,x^3+x^2,x^4])),
  true)$


okltest_rijn_m2natl(f) := (
    assert(f(diagmatrix(4,0)) = create_list(0,i,1,16)),
  assert(f(genmatrix(lambda([a,b],1),4,4)) = create_list(1,i,1,16)),
  assert(f(matrix(
      [1,x^2+1,x^3+1,x^3+x^2+1],
      [x,x^2+x,x^3+x,x^3+x^2+x],
      [x+1,x^2+x+1,x^3+x+1,x^3+x^2+x+1],
      [x^2,x^3,x^3+x^2,x^4])) = [1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16]),
  true)$

okltest_ss_natl2m(f) := (
  assert(okltest_rijn_natl2m(buildq([f], lambda([L], f(L,2,4))))),
  true)$

okltest_ss_m2natl(f) := (
  assert(okltest_rijn_m2natl(buildq([f], lambda([L], f(L,2))))),
  true)$

okltest_ss_matrix2hex(f) := block(
  assert(f(matrix([1]), 2, 4, ss_polynomial_2_4) = "1"),
  assert(f(matrix([1],[0]), 2, 4, ss_polynomial_2_4) = "10"),
  assert(f(matrix([1,x],[0,x^3]), 2, 4, ss_polynomial_2_4) = "1028"),
  assert(f(matrix([x^6+x^5+x^2+x,x^7+x^6+x^5+x^3+x^2+x+1,x^7+x^3,x^7+x^6+x^3+x],
        [x^7+x^6+x^5+x^3+1,x^7+x^3+x,x^6+x^3+x^2,x^5+x^4+x^2],
        [x^6+x^3+x+1,x^5+x^3+x^2,x^7+x^6+x^5+x^4+x^3+x,x^5+x^3+x+1],
        [x^7+x^6+x^4+x^2,x^5+x^4+x^3+x+1,x^6+x^4+x^3+1,x^5+x^3+x^2+x]),2,8,
    ss_polynomial_2_8) = "66E94BD4EF8A2C3B884CFA59CA342B2E"),
  true)$

okltest_ss_hex2matrix(f) := block(
  assert(f("1", 2, 4, ss_polynomial_2_4, 1) = matrix([1])),
  assert(f("10",  2, 4, ss_polynomial_2_4, 2) = matrix([1],[0])),
  true)$

/*!
\endhtmlonly
*/