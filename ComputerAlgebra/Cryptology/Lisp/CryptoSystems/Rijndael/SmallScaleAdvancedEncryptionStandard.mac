/* Matthew Gwynne, 27.1.2010 (Swansea) */
/* Copyright 2010, 2011 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/SmallScaleAdvancedEncryptionStandard.mac
  \brief Implementing the small-scale variation of AES, and generalisations

Use by

oklib_load("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/SmallScaleAdvancedEncryptionStandard.mac");

*/

/*!
\htmlonly
*/

/* The following file is guaranteed to be included: */
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/SmallScaleWordFields.mac")$

oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/CombinatorialMatrices/Lisp/Basics.mac")$
oklib_plain_include(eigen)$ /* (for function columnvector) */


/*
   The following implements the small scale AES as described in [Small Scale
   Variants of the AES] and
   [Algebraic Aspects of the Advanced Encryption Standard, section 3.3].

   The basic structure of the encryption and decryption is the same as AES
   (see AdvancedEncryptionStandard.mac). In particular matrices are used
   as input and output, and polynomials are used as the block elements.

   However, rather than simply being able to vary the number of rounds, this
   system allows one to additionally vary the following parameters (all
   non-negative integers):

   e - specifies that the size of the finite field used for individual
       block elements should be 2^e (i.e., the field is a GF(2^e) field)
       where the modulo polynomials for each of these fields is
       predefined.
   n_C - The number of columns in the block matrix.
   n_R - The number of rows in the block matrix.

   In addition to these parameters, we additionally (to [Small Scale Variants
   of the AES]) allow arbitrary finite fields rather than just fields of size
   2^e for the block elements and therefore we also introduce an integer
   parameter b for the base, and a parameter mod_poly for the
   modulo-polynomial to define (and standardise) the result of any operations
   in this field.

   Given this variety of parameters, functions are needed to define
   different Sbox and MixColumn operations based on the new dimensions of the
   fields and the block, which in turn use different vectors and matrices
   in their definition (as defined by [Small Scale Variants of the AES] or by
   the user in cases where this has not been defined there), therefore
   functions such as ss_mixcolumn_matrix, ss_sbox_matrix,
   ss_sbox_affine_constant etc. are defined which take their defining
   parameters (from b,e,n_C,n_R) and return the "standard" object to be used
   in each case.

   In [Small Scale Variants of the AES], there are two major variations, apart
   from the minor variations described above, namely those denoted by AES (the
   default variation in [Small Scale Variants of the AES]) and AES*, where AES
   is defined without a specialised final round (contrary to the standard AES
   definition), while AES* is defined in the same way as the standard AES with
   a specialised final round which does not contain MixColumns.

   For some functions two variants are provided, the standard version (without
   any special ending) and a generalised version (postfixed with "_gen"):
    - The standard versions take (in addition to any other arguments) only the
      base b and exponent e for the field, and then all other parameters
      (mod_poly, the sbox function, mixcolumn function etc) are derived using
      functions such as ss_polynomial, ss_sbox etc which provide the default
      constants and implementations.
    - The generalised version then take all parameters necessary for their
      function (from the list of parameters described above) without
      assuming any defaults.
   
*/

/*
   The following naming conventions have been used in this module:

   Functions ending in "_gen" provide a generalisation of their namesake
   function, where additional parameters are taken, rather than using the
   standard constants provided by functions such as ss_mixcolumn_matrix etc.

   The AES and AES* variations as described in [Small scale variants of the
   AES] are called given by the functions ss_encrypt and ss_encrypt_wf
   respectively where "_wf" signifies "*w*ith *f*inal round".

*/

/* TODO: REWRITE documentation below */
/* TODO: TIDY code below to ensure readability. */

/* OK: I thought this file would have been completed? */

/* **************************************
   * Special bit-linear byte operations *
   **************************************
*/

/* Here we provide special operations at the byte level involving linear
   transformations at the bit level. */

/* Returns the result of applying the sboxes linear map to the given
   word (as a polynomial). */
ss_sbox_linmap_gen(field_element,b,e,mod_poly, sbox_matrix) :=
  ss_stand(mvec2poly(sbox_matrix . poly2mvec_ss(field_element,e)),b,mod_poly)$
ss_sbox_linmap(field_element,b,e) :=
  ss_sbox_linmap_gen(field_element,b,e,ss_polynomial(b,e),ss_sbox_matrix(b,e))$

/* Returns the result of applying the sboxes linear map and then multiplication
   by a to the given word (as a polynomial). */
ss_mul_w_sbox_linmap_gen(field_element,a,b,e,mod_poly, sbox_matrix) :=
  ss_mul(
    ss_sbox_linmap_gen(field_element,b,e,mod_poly,sbox_matrix),a,b,mod_poly)$
ss_mul_w_sbox_linmap(field_element,a,b,e) :=
  ss_mult_w_sbox_linmap_gen(
    field_element,a,b,e,ss_polynomial(b,e),ss_sbox_matrix(b,e))$

/* *********
   * S box *
   *********
*/

/* Constant used in the affine transformation within ss_sbox: */
ss_affine_constant_2_4 : '(x^2+x)$
ss_affine_constant_2_8 : '(x^6 + x^5 + x + 1)$
/* Returns the default affine constant for the default GF(b^e) field: */
ss_affine_constant(b,e) :=
  if b = 2 then
    if e = 4 then ss_affine_constant_2_4
    else if e = 8 then ss_affine_constant_2_8
    else und
  else und$
/* Constant used in the affine transformation within ss_inv_sbox: */
/* Generated by multiplying the corresponding Sbox matrix by the
   vector for the affine constant and converting back to polynomials. */
ss_inv_affine_constant_2_4 : '(x+1)$ 
ss_inv_affine_constant_2_8 : '(x^2 + 1)$
/* Returns the default inverse affine constant for the default GF(b^e) field. */
ss_inv_affine_constant(b,e) :=
  if b = 2 then
    if e = 4 then ss_inv_affine_constant_2_4
    else if e = 8 then ss_inv_affine_constant_2_8
    else und
  else und$

/* Matrix used in the affine transformation within ss_sbox: */
ss_sbox_matrix_2_1 : matrix([1])$
ss_sbox_matrix_2_4 : apply(matrix,
  create_list(rotate([1,0,1,1],i),i,0,3))$
ss_sbox_matrix_2_8 : apply(matrix,
  create_list(rotate([1,1,1,1,1,0,0,0],i),i,0,7))$
/* Returns the default sbox matrix for the default GF(b^e) field. */
ss_sbox_matrix(b,e) :=
  if b = 2 then
    if e = 1 then ss_sbox_matrix_2_1
    else if e = 4 then ss_sbox_matrix_2_4
    else if e = 8 then ss_sbox_matrix_2_8
    else und
  else und$
/* Matrix used in the affine transformation within ss_sbox: */
ss_inv_sbox_matrix_2_1 : matrix([1])$
ss_inv_sbox_matrix_2_4 : apply(matrix,
  create_list(rotate([1,1,1,0],i),i,0,3))$
ss_inv_sbox_matrix_2_8 : apply(matrix,
  create_list(rotate([0,1,0,1,0,0,1,0],i),i,0,7))$
/* Returns the default inverse sbox matrix for the default GF(b^e) field. */
ss_inv_sbox_matrix(b,e) :=
  if b = 2 then
    if e = 1 then ss_inv_sbox_matrix_2_1
    else if e = 4 then ss_inv_sbox_matrix_2_4
    else if e = 8 then ss_inv_sbox_matrix_2_8
    else und
  else und$

/* The small scale AES sbox takes an individual small scale
   field element (an element of the block) and returns
   the result of applying the small scale sbox operation
   defined for the given field parameters, sbox_matrix
   and affine_constant. Note here that sbox_matrix is a
   diagonal matrix of size e of elements in {1,...,b} and
   affine constant is a column vector size e with elements
   in {1,...,b}.

   Formally given a polynomial p as the field_element, the
   result of ss_sbox_gen is the polynomial of the form:

   sum(b[i] * x^(e-i), i, 1,e)

   where

   b[i] = sum(sbox_matrix[i,j] * a[j], i, 1, e) + affine_constant[i]

   and the coefficients a[i] (for i in {1,...,e}) are the coefficients
   of the polynomial defined as the inverse of p in the field:

   sum(a[i] * x^(e-i),i,1,e) = p^(-1) (in GF(b^e) mod mod_poly)

   where a[i],b[i] range over {0,...,b}.

   ss_inv_sbox(,_gen) are defined precisely the same as ss_sbox(,_gen)
   but for convention/convenience another function is provided with
   the expectation that the inv_sbox_matrix provided is the inverse
   of the matrix provided to ss_sbox(,_gen) within the GF(b^e) field.   
*/
ss_sbox_gen(field_element,b,e,mod_poly, sbox_matrix,affine_constant) := block(
  [inv_elem : ss_einv(field_element,b,mod_poly)],
  ss_stand(mvec2poly(
    sbox_matrix . poly2mvec_ss(inv_elem,e)) + affine_constant,b,mod_poly))$
ss_sbox(field_element, b,e) :=
  ss_sbox_gen(field_element,b,e,ss_polynomial(b,e),
    ss_sbox_matrix(b,e),ss_affine_constant(b,e))$
ss_inv_sbox_gen(field_element,b,e,mod_poly,
    inv_sbox_matrix,inv_affine_constant) := block(
  [affine_result : mvec2poly(
      inv_sbox_matrix . poly2mvec_ss(field_element,e) +
          poly2mvec_ss(inv_affine_constant,e))],
  ss_einv(affine_result,b,mod_poly))$
ss_inv_sbox(field_element, b,e) :=
  ss_inv_sbox_gen(field_element,b,e,ss_polynomial(b,e),
    ss_inv_sbox_matrix(b,e),ss_inv_affine_constant(b,e))$


/* Small scale Sbox as boolean function */
ss_sbox_bf(V,b,e) := poly2vec_ss(ss_sbox(vec2poly(V),b,e),e)$
/* TODO: implement general versions of these functions. */

/* Sbox with addition as a boolean function where the first half of
   the input boolean vector is input to the Sbox and then the result of
   the Sbox, is added to the other half of the input. */
ss_sbox_w_add_bf(V,b,e) := block(
  [V1 : take_elements(floor(length(V)/2),V), V2 : rest(V,floor(length(V)/2))],
  ss_vecadd(ss_sbox_bf(V1,b,e),V2,b))$

/* Sbox followed by multiplication by word field element elem as a boolean 
   function where the first half of the input boolean vector is input to the 
   Sbox and then the result of the Sbox, is added to the other half of the 
   input. */
ss_sbox_w_mul_bf(V,elem,b,e) :=
  ss_vecmul(ss_sbox_bf(V,b,e),poly2vec_ss(elem,e),b,e,ss_polynomial(b,e))$


/* *************
   * Sub-bytes *
   *************
*/

/* 
   Takes the input block as a matrix of arbitrary polynomials
   and applies the given sbox operation to each polynomial, returning
   the matrix of result polynomials:
*/
ss_subbytes(inputmatrix, sbox_f) := matrixmap(sbox_f, inputmatrix)$

/* 
   Takes the input block as a matrix of polynomials and applies the given
   inverse sbox operation to each polynomial, returning the list of result
   polynomials:
*/
ss_inv_subbytes(inputmatrix,inv_sbox_f) := matrixmap(inv_sbox_f, inputmatrix)$


/* **************
   * Shift rows *
   **************
*/


/*
   Takes a matrix and performs the shiftrows operation,
   returning a matrix where row i (for i in {1,..,length(inputmatrix)) has
   been shifted cyclically left by i-1.
*/
ss_shiftrows(inputmatrix) :=
  apply(matrix,
    create_list(rotate(inputmatrix[abs(r)+1],-r), r, 0,length(inputmatrix)-1))$


/*
   Takes a matrix and performs the inverse shiftrows operation,
   returning a matrix where row i (for i in {1,..,length(inputmatrix)) has
   been shifted cyclically right by i -1.
*/
ss_inv_shiftrows(inputmatrix) :=
  apply(matrix,
    create_list(rotate(inputmatrix[abs(r)+1],r), r, 0,length(inputmatrix)-1))$


/* ***************
   * Mix columns *
   ***************
*/

/*
   Matrices used in the mixcolumns step to model the 
   multiplication of each n_C-byte column by a constant
   in a polynomial ring of at most order n_C with coefficients in
   GF(b^e):
*/
ss_mixcolumns_matrix_2_4_1 : matrix([1])$
ss_mixcolumns_matrix_2_8_1 : matrix([1])$
ss_mixcolumns_matrix_2_4_2 : block([x], apply(matrix,
  create_list(rotate([x+1,x],i),i,0,1)))$
ss_mixcolumns_matrix_2_8_2 : block([x], apply(matrix,
  create_list(rotate([x+1,x],i),i,0,1)))$
ss_mixcolumns_matrix_2_4_4 : block([x], apply(matrix, 
  create_list(rotate([x, x + 1, 1, 1], i),i,0,3)))$
ss_mixcolumns_matrix_2_8_4 : block([x], apply(matrix, 
  create_list(rotate([x, x + 1, 1, 1], i),i,0,3)))$
ss_mixcolumns_matrix(b,e,n_R) :=
  if b = 2 then
    if n_R = 4 then
      if e = 4 then ss_mixcolumns_matrix_2_4_4
      else if e = 8 then ss_mixcolumns_matrix_2_8_4
      else und
    else if n_R = 2 then
      if e = 4 then ss_mixcolumns_matrix_2_4_2
      else if e = 8 then ss_mixcolumns_matrix_2_8_2
      else und
    else if n_R = 1 then
      if e = 4 then ss_mixcolumns_matrix_2_4_1
      else if e = 8 then ss_mixcolumns_matrix_2_8_1
      else und
    else und
  else und$
/* Inverse of the rijn_mix_columns_matrix: */
ss_inv_mixcolumns_matrix_2_4_1 : matrix([1])$
ss_inv_mixcolumns_matrix_2_8_1 : matrix([1])$
ss_inv_mixcolumns_matrix_2_4_2 : block([x], apply(matrix,
  create_list(rotate([x+1,x], i),i,0,1)))$
ss_inv_mixcolumns_matrix_2_8_2 : block([x], apply(matrix,
  create_list(rotate([x+1,x], i),i,0,1)))$
ss_inv_mixcolumns_matrix_2_4_4 : block([x], apply(matrix,
  create_list(rotate([x^3+x^2+x,x^3+x+1,x^3+x^2+1,x^3+1], i),i,0,3)))$
ss_inv_mixcolumns_matrix_2_8_4 : block([x], apply(matrix,
  create_list(rotate([x^3+x^2+x,x^3+x+1,x^3+x^2+1,x^3+1], i),i,0,3)))$
ss_inv_mixcolumns_matrix(b,e,n_R) :=
  if b = 2 then
    if n_R = 4 then
      if e = 4 then ss_inv_mixcolumns_matrix_2_4_4
      else if e = 8 then ss_inv_mixcolumns_matrix_2_8_4
      else und
    else if n_R = 2 then
      if e = 4 then ss_inv_mixcolumns_matrix_2_4_2
      else if e = 8 then ss_inv_mixcolumns_matrix_2_8_2
      else und
    else if n_R = 1 then
      if e = 4 then ss_inv_mixcolumns_matrix_2_4_1
      else if e = 8 then ss_inv_mixcolumns_matrix_2_8_1
      else und
    else und
  else und$

ss_mixcolumns_matrix2inv_mixcolumns_matrix(b,e, mixcolumns_matrix) :=
  if b = 2 then
    if e = 8 then
      if mixcolumns_matrix = ss_mixcolumns_matrix_2_8_4 then
        ss_inv_mixcolumns_matrix_2_8_4
      else if mixcolumns_matrix = ss_mixcolumns_matrix_2_8_2 then
        ss_inv_mixcolumns_matrix_2_8_2
      else if mixcolumns_matrix = ss_mixcolumns_matrix_2_8_1 then
        ss_inv_mixcolumns_matrix_2_8_1
      else und
    else if e = 4 then
      if mixcolumns_matrix = ss_mixcolumns_matrix_2_4_4 then
        ss_inv_mixcolumns_matrix_2_4_4
      else if mixcolumns_matrix = ss_mixcolumns_matrix_2_4_2 then
        ss_inv_mixcolumns_matrix_2_4_2
      else if mixcolumns_matrix = ss_mixcolumns_matrix_2_4_1 then
        ss_inv_mixcolumns_matrix_2_4_1
      else und
    else und
  else und$
      

/* Takes a (maxima) vector of polynomials and returns the result of
   multiplying the input matrix by the given mixcolumns_matrix
   in field specified by b, e and mod_poly.

   So for an input vector V of size n_R, the result is a vector W such that
   for i in {1,...,n_R}

   W[i] = sum(ss_stand(mixcolumns_matrix[i,j] * V[i]),b,e,mod_poly),j,1,n_R)

   where ss_stand is the standardisation of the result of the multiplication
   into a polynomial of the form sum(a[i] * x^(e-i), i, 1,e) where each a[i]
   is in {1,...,b}, i.e., a standardised representation of GF(b^e) field
   elements.
*/
ss_mixcolumn_gen(V,b,e,mod_poly,mixcolumns_matrix) :=
  matrixmap(lambda([p],ss_stand(p,b,mod_poly)),
      mixcolumns_matrix . V);
ss_mixcolumn(V,b,e) :=
  ss_mixcolumn_gen(V,b,e,
    ss_polynomial(b,e),
    ss_mixcolumns_matrix(b,e,length(V)))$

/* The small scale MixColumns operation as a boolean function: */
ss_mixcolumn_gen_bf(V,b,e,mod_poly,mixcolumns_matrix) := block(
  [Vec : map(vec2poly,partition_elements(V,e))],
  lappend(
    m2l_r(matrixmap(lambda([E],poly2vec_ss(E,e)),
        ss_mixcolumn_gen(Vec,b,e,mod_poly,mixcolumns_matrix)))))$
ss_mixcolumn_bf(V,b,e) := 
  ss_mixcolumn_gen_bf(V,b,e,ss_polynomial(b,e),
    ss_mixcolumns_matrix(b,e,floor(length(V)/e)))$

/* The boolean matrix representing the MixColumns operation  */
ss_mixcolumn_gen_boolm(b,e,mod_poly,mixcolumns_matrix) := block([rows],
  rows : length(mixcolumns_matrix),
  transpose(apply(matrix,
      create_list(
        ss_mixcolumn_gen_bf(
          create_list(if i = j then 1 else 0, j, 1, e * rows),
          b,e,mod_poly, mixcolumns_matrix),
        i,1,e*rows))))$
ss_mixcolumn_boolm(b,e,rows) :=
  ss_mixcolumn_gen_boolm(b,e,ss_polynomial(b,e),
    ss_mixcolumns_matrix(b,e,rows))$

/* The small scale SubBytes followed by MixColumns operation on a single
   column of the AES block as a boolean function: */
ss_round_column_gen_bf(V,b,e,mod_poly,mixcolumns_matrix,sbox_f_) := block(
  [Vec : map(vec2poly,partition_elements(V,e))],
  lappend(
    m2l_r(matrixmap(lambda([E],poly2vec_ss(E,e)),
          ss_mixcolumn_gen(
            ss_subbytes(Vec,sbox_f_),b,e,mod_poly,mixcolumns_matrix)))))$
ss_round_column_bf(V,b,e) := 
  ss_round_column_gen_bf(V,b,e,ss_polynomial(b,e),
    ss_mixcolumns_matrix(b,e,floor(length(V)/e)),
    lambda([p],ss_sbox(p,b,e)))$

/* Takes a (maxima) matrix of polynomials and returns the result of
   multiplying the input matrix by the given mixcolumns_matrix
   in field specified by b, e and mod_poly.

   So for an input matrix M of size n_R, the result is a vector W such that
   for i in {1,...,n_R}, j in {1,...,n_C}

   N[i,j] = sum(ss_stand(mixcolumns_matrix[i,k] * M[i,j]),b,e,mod_poly),
                  k,1,n_R)

   where ss_stand is the standardisation of the result of the multiplication
   into a polynomial of the form sum(a[i] * x^(e-i), i, 1,e) where each a[i]
   is in {1,...,b}, i.e., a standardised representation of GF(b^e) field
   elements.
*/
ss_mixcolumns_gen(M,b,e,mod_poly,mixcolumns_matrix) :=
  matrixmap(lambda([p],ss_stand(p,b,mod_poly)),
      mixcolumns_matrix . M);
ss_mixcolumns(M,b,e) :=
  ss_mixcolumns_gen(M,b,e,
    ss_polynomial(b,e),
    ss_mixcolumns_matrix(b,e,length(M)))$

/* Takes a (maxima) vector of polynomials and returns the result of
   multiplying the input matrix by the given mixcolumns_matrix
   in field specified by b, e and mod_poly.

   So for an input vector V of size n_R, the result is a vector W such that
   for i in {1,...,n_R}

   W[i] = sum(ss_stand(mixcolumns_matrix[i,j] * V[i]),b,e,mod_poly),j,1,n_R)

   where ss_stand is the standardisation of the result of the multiplication
   into a polynomial of the form sum(a[i] * x^(e-i), i, 1,e) where each a[i]
   is in {1,...,b}, i.e., a standardised representation of GF(b^e) field
   elements.
*/
ss_inv_mixcolumn_gen(V,b,e,mod_poly,inv_mixcolumns_matrix) :=
  matrixmap(lambda([p],ss_stand(p,b,mod_poly)),
      inv_mixcolumns_matrix . V);
ss_inv_mixcolumn(V,b,e) :=
  ss_inv_mixcolumn_gen(V,b,e,
    ss_polynomial(b,e),
    ss_inv_mixcolumns_matrix(b,e,length(V)))$

/* Takes a (maxima) matrix of polynomials and returns the result of
   multiplying the input matrix by the given mixcolumns_matrix
   in field specified by b, e and mod_poly.

   So for an input matrix M of size n_R, the result is a vector W such that
   for i in {1,...,n_R}, j in {1,...,n_C}

   N[i,j] = sum(ss_stand(inv_mixcolumns_matrix[i,k] * M[i,j]),b,e,mod_poly),
                  k,1,n_R)

   where ss_stand is the standardisation of the result of the multiplication
   into a polynomial of the form sum(a[i] * x^(e-i), i, 1,e) where each a[i]
   is in {1,...,b}, i.e., a standardised representation of GF(b^e) field
   elements.
*/
ss_inv_mixcolumns_gen(M,b,e,mod_poly,inv_mixcolumns_matrix) :=
  matrixmap(lambda([p],ss_stand(p,b,mod_poly)),
      inv_mixcolumns_matrix . M);
ss_inv_mixcolumns(M,b,e) :=
  ss_mixcolumns_gen(M,b,e,
    ss_polynomial(b,e),
    ss_inv_mixcolumns_matrix(b,e,length(M)))$


/* *****************
   * Key expansion *
   *****************
*/

/*
   Takes a matrix of polynomials as the round key for
   round r-1, and returns the small scale AES round
   key for round r, given the small scale sbox function
   sbox_f.
*/
/* OK: what is the *mathematical* definition? */
ss_keyschedule(M, r, b, mod_poly, sbox_f) := block(
  [newcols : matrix(), n_C : length(matrixcolumns(M)),
   round_constant : x^(r-1), n_R : length(M)],
  newcols : addcol(newcols,columnvector(
      create_list(
        (if n_C > 1 then M[i,1] else 0) + sbox_f(M[mod(i,n_R)+1,n_C]) + 
        if i = 1 then round_constant else 0,i,1,n_R))),
  for j : 2 thru n_C do
    newcols : addcol(newcols,columnvector(
      create_list(M[i,j] + newcols[i,j-1],i,1,n_R))),
  return(matrixmap(lambda([p],ss_stand(p,b,mod_poly)),newcols)))$

/*
   Takes a matrix of polynomials as the input key and
   returns a matrix of size n_R * n_C * (num_rounds+1)
   where columns i * (n_R * n_C) to (i+1) * (n_R * N_C)
   are the round key matrix for round i, and where n_R and
   n_C are the number of rows and number of columns in
   the input key matrix (resp).
*/
ss_key_expansion_gen(M, num_rounds, b, mod_poly, sbox_f) := block(
  [ks : lambda([M,r],
    endcons(ss_keyschedule(last(M),r,b,mod_poly,sbox_f),M))],
  lreduce(ks, create_list(i,i,1,num_rounds), [M]))$
ss_key_expansion(M,num_rounds,b,e) := 
  ss_key_expansion_gen(M,num_rounds,b,ss_polynomial(b,e), lambda([a],ss_sbox(a,b,e)))$


/* *********************************************
   * Small scale AES encryption and decryption *
   *********************************************
*/

/* Takes a matrix of polynomials and another matrix
   of polynomials representing the round key and computes the result of
   applying the small scale AES round function on this matrix
   given the sbox and mixcolumn functions.

   Note ss_round_wa does not take the round key, and performs
   all operations in the small scale AES round, apart from the
   addition of the round key (i.e., subbytes, shiftrows, mixcolumns).
*/
ss_round_wa_gen(pl,b,e,mod_poly,sbox_f,mixcolumns_matrix) := 
    ss_mixcolumns_gen(ss_shiftrows(
        ss_subbytes(pl,sbox_f)),b,e,mod_poly,mixcolumns_matrix)$
ss_round_wa(pl,b,e) := 
    ss_mixcolumns(ss_shiftrows(
        ss_subbytes(pl,lambda([a],ss_sbox(a,b,e)))),b,e)$
ss_round_gen(pl, rkl,b,e,mod_poly,sbox_f,mixcolumns_matrix) := 
  matrixmap(lambda([p], ss_stand(p,b,mod_poly)),
    ss_round_wa_gen(pl,b,e,mod_poly,sbox_f,mixcolumns_matrix)+rkl)$
ss_round(pl,rkl,b,e) := 
  matrixmap(lambda([p], ss_stand(p,b,ss_polynomial(b,e))),
    ss_round_wa(pl,b,e)+rkl)$
  
/* Takes a matrix of polynomials and another matrix
   of polynomials representing the round key and computes the result of
   applying the inverse of the small scale AES round function on this matrix
   given the sbox and mixcolumn functions.


   Note ss_inv_round_wa does not take the round key, and performs
   all operations in the small scale AES round, apart from the
   addition of the round key (i.e., inv_subbytes, inv_shiftrows,
   inv_mixcolumns).
*/
ss_inv_round_wa_gen(pl,b,e,mod_poly,inv_sbox_f,inv_mixcolumn_matrix) := 
    ss_inv_subbytes(
      ss_inv_shiftrows(
        ss_inv_mixcolumns_gen(pl,b,e,mod_poly,inv_mixcolumns_matrix)),
      inv_sbox_f)$
ss_inv_round_wa(pl,b,e,mod_poly,inv_sbox_f,inv_mixcolumn_matrix) := 
    ss_inv_subbytes(ss_inv_shiftrows(ss_inv_mixcolumns(pl,b,e)),inv_sbox_f)$
ss_inv_round_gen(pl,rkl,b,e,mod_poly,inv_sbox_f,inv_mixcolumns_matrix) := 
    ss_inv_round_wa(pl+rkl,b,e,mod_poly,inv_sbox_f, inv_mixcolumns_matrix)$
ss_inv_round(pl,rkl,b,e,mod_poly,inv_sbox_f,inv_mixcolumns_matrix) := 
    ss_inv_round_wa(pl+rkl,b,e,mod_poly,inv_sbox_f, inv_mixcolumns_matrix)$


/* Takes a matrix of polynomials and another matrix
   of polynomials representing the round key and computes the result of
   applying the specialised final small scale AES round function on this
   matrix given the sbox and mixcolumn functions.

   Note ss_final_round_wa does not take the round key, and performs
   all operations in the small scale AES round, apart from the
   addition of the round key (i.e., subbytes, shiftrows).
*/
ss_final_round_wa(pl,sbox_f) := /* Without round key addition */
    ss_shiftrows(
        ss_subbytes(pl,sbox_f))$
ss_final_round(pl,rkl,b,mod_poly, sbox_f) :=  /* With round key addition */
  matrixmap(lambda([p],ss_stand(p,b,mod_poly)),
    ss_final_round_wa(pl,sbox_f)+rkl)$
/* Takes a matrix of polynomials and another matrix
   of polynomials representing the round key and computes the result of
   applying the inverse of the specialised final small scale AES round
   function on this  matrix given the sbox and mixcolumn functions.

   Note ss_inv_final_round_wa does not take the round key, and performs
   all operations in the small scale AES round, apart from the
   addition of the round key (i.e., inv_subbytes, inv_shiftrows).
*/
ss_inv_final_round_wa(pl,inv_sbox_f) := 
    ss_inv_subbytes(ss_inv_shiftrows(pl), inv_sbox_f)$
ss_inv_final_round(pl,rkl,inv_sbox_f) := 
    ss_inv_final_round_wa(pl+rkl, inv_sbox_f)$


/* Takes a matrix of polynomials, another matrix
   of polynomials representing the round key, and the number of rounds and
   computes the result (as a matrix of polynomials) of applying the small
   scale AES on this  matrix given the sbox and mixcolumn functions.

   Note ss_encrypt_wf(,_gen) use the standard round function for the final
   round, not the specialised version.
*/
/* WITH final round (_wf) */
ss_encrypt_wf_gen(pl, kl, num_rounds,b,e,mod_poly,sbox_f,mixcolumns_matrix) :=
block([n_C : length(pl)],
  /* Initial Rounds */
  ekl : ss_key_expansion_gen(kl,num_rounds,b,mod_poly,sbox_f),
  initial_rk : first(ekl), final_rk : last(ekl),
  initial_result : 
    lreduce(lambda([m,n],
        ss_round_gen(m,n,b,e,mod_poly,sbox_f,mixcolumns_matrix)),
    rest(rest(ekl), -1), pl + initial_rk),
  /* Final Round */
  matrixmap(lambda([p],ss_stand(p,b,mod_poly)),
      ss_final_round(initial_result,final_rk, b,mod_poly, sbox_f))
)$
ss_encrypt_gen(pl, kl, num_rounds,b,e,mod_poly, sbox_f, mixcolumns_matrix) :=
block(
  [n_C : length(pl)],
  /* Initial Rounds */
  ekl : ss_key_expansion_gen(kl,num_rounds,b,mod_poly,sbox_f),
  initial_rk : first(ekl),
  matrixmap(lambda([p],ss_stand(p,b,mod_poly)),
    lreduce(lambda([m,n],
        ss_round_gen(m,n,b,e,mod_poly,sbox_f,mixcolumns_matrix)),
    rest(ekl), pl + initial_rk))
)$


/* Takes a matrix of polynomials, another matrix
   of polynomials representing the round key, and the number of rounds and
   computes the result (as a matrix of polynomials) of applying the small
   scale AES decryption on this  matrix given the sbox, inverse sbox and
   inverse  mixcolumn functions.

   Note ss_decrypt_wf(,_gen) use the standard inverse round function for the
   final round, not the specialised version.
*/
/* WITH final round (_wf) */
ss_decrypt_wf_gen(pl, kl, num_rounds, b,e,mod_poly, sbox_f, inv_sbox_f,inv_mixcolumns_matrix) := block(
  [n_C : length(pl)],
  /* Initial Rounds */
  ekl : ss_key_expansion_gen(kl,num_rounds,b,mod_poly,sbox_f),
  initial_rk : first(ekl), final_rk : last(ekl),
  /* Final Round */
  if num_rounds = 10 then
    initial_result : ss_inv_final_round(pl,final_rk,inv_sbox_f)
  else
    initial_result :
      ss_inv_round_gen(pl,final_rk,b,e,mod_poly,
        inv_sbox_f,inv_mixcolumn_matrix),
  matrixmap(lambda([a],ss_stand(a,b,mod_poly)),
    rreduce(
      lambda([m,n],
        ss_inv_round_gen(m,n,b,e,mod_poly,inv_sbox_f,inv_mixcolumn_matrix)), 
      rest(rest(ekl), -1), initial_result) + initial_rk)
)$
/* WITHOUT final round */
ss_decrypt_gen(pl, kl, num_rounds, b,e,mod_poly, sbox_f, inv_sbox_f,inv_mixcolumns_matrix) := block(
  [n_C : length(pl)],
  /* Initial Rounds */
  ekl : ss_key_expansion_gen(kl,num_rounds,b,mod_poly,sbox_f),
  initial_rk : first(ekl), 
  matrixmap(lambda([a],ss_stand(a,b,mod_poly)),
    rreduce(
      lambda([m,n],
        ss_inv_round_gen(m,n,b,e,mod_poly,inv_sbox_f,inv_mixcolumns_matrix)), 
      rest(ekl), pl) + initial_rk)
)$


/* **********************************************************
   * Small scale AES encryption as an iterated block cipher *
   **********************************************************
*/

/* Combined small scale round function to be used within the
   iterated block cipher implementation of the small scale AES
   (ss_encrypt_ibc_gen).

   For the given plaintext and round, ss_round_ibc_gen acts
   identically on plain_text if round is 0, and otherwise
   acts as the small scale AES round function (without key
   addition).

   The reason for the difference when round is 0, is so that
   the iterated block cipher scheme applies the initial round
   key addition required by the AES, before iterating the round
   function.
*/
ss_round_ibc_gen(plain_text, round,b,e,mod_poly, sbox_f,mixcolumns_matrix) :=
  if round = 0 then plain_text
  else
    ss_round_wa_gen(plain_text,b,e,mod_poly, sbox_f, mixcolumns_matrix)$

/* Small scale AES encryption as ibc (see
   Cryptology/Lisp/CryptoSystems/IteratedBlockCipher.mac): */
ss_encrypt_ibc_gen(plaintext, key, num_rounds, b,e,mod_poly,
  sbox_f, mixcolumns_matrix) :=
    ibc_0(
      buildq([b,mod_poly,sbox_f],
        lambda([key,r], ss_keyschedule(key,r,b,mod_poly,sbox_f))),
      buildq([sbox_f,mixcolumns_matrix,b,e,mod_poly],
        lambda([p,r],
          ss_round_ibc_gen(p,r,b,e,mod_poly,sbox_f,mixcolumns_matrix))),
      buildq([b,mod_poly],
        lambda([m,n],
          matrixmap(lambda([p],ss_stand(p,b,mod_poly)),m+n)))
      )(plaintext,key,num_rounds+1)$


/* **********************************************************
   * Small scale AES instantiations                         *
   **********************************************************
*/

/* Default small scale AES encryption WITHOUT a final round: */
ss_encrypt(pl, kl, num_rounds,b,e) :=
  ss_encrypt_gen(pl,kl,num_rounds,b,e,
    ss_polynomial(b,e),
    buildq([b,e],lambda([a], ss_sbox(a,b,e))),
    ss_mixcolumns_matrix(b,e,length(pl)))$
/* Default small scale AES encryption WITH final round (_wf): */
ss_encrypt_wf(pl, kl, num_rounds,b,e) :=
   ss_encrypt_wf_gen(pl,kl,num_rounds,b,e,
     ss_polynomial(b,e), lambda([a], ss_sbox(a,b,e)),
     ss_mixcolumns_matrix(b,e,length(pl)))$
/* Default small scale AES decryption WITHOUT final round: */
ss_decrypt(pl, kl, num_rounds, b,e) :=
  ss_decrypt_gen(pl,kl,num_rounds,b,e,ss_polynomial(b,e),
    buildq([b,e],lambda([a], ss_sbox(a,b,e))),
    buildq([b,e],lambda([a], ss_inv_sbox(a,b,e))),
    ss_inv_mixcolumns_matrix(b,e,length(pl)))$
/* Default small scale AES decryption WITH final round (_wf): */
ss_decrypt_wf(pl, kl, num_rounds, b,e) :=
  ss_decrypt_wf_gen(pl,kl,num_rounds,b,e,ss_polynomial(b,e),
    lambda([a], ss_sbox(a,b,e)), lambda([a], ss_inv_sbox(a,b,e)),
    ss_inv_mixcolumns_matrix(b,e,length(pl)))$

/* Instantiation of AES as an iterated block cipher: */
ss_encrypt_ibc(plaintext,key,num_rounds,b,e) :=
  ss_encrypt_ibc_gen(plaintext,key,num_rounds,b,e,ss_polynomial(b,e),
    buildq([b,e],lambda([p],ss_sbox(p,b,e))),
    ss_mixcolumns_matrix(b,e,length(plaintext)))$


/* Input and output as lists of natural numbers from 0 to b^e-1: */
ss_encrypt_natl(pl, kl, num_rounds, b, e, n_R) :=
  ss_m2natl(
    ss_encrypt(ss_natl2m(pl,b,n_R),ss_natl2m(kl,b,n_R),num_rounds,b,e),b)$
ss_decrypt_natl(pl, kl, num_rounds, b, e, n_R) :=
  ss_m2natl(
    ss_decrypt(ss_natl2m(pl,b,n_R),ss_natl2m(kl,b,n_R),num_rounds,b,e),b)$

/* Input and output as lists of binary numbers of size e*n_C*n_R: */
ss_encrypt_bin(pl, kl, num_rounds,b,e,n_R) :=
  lappend(
    map(lambda([m],int2polyadic_padd(m,b,e)),
      ss_encrypt_natl(
        map(binv2int, partition_elements(pl,e)),
        map(binv2int, partition_elements(kl,e)),
        num_rounds, b, e, n_R)))$
ss_decrypt_bin(pl, kl, num_rounds,b,e,n_R) :=
  lappend(
    map(lambda([m],int2polyadic_padd(m,b,e)),
      ss_decrypt_natl(
        map(binv2int, partition_elements(pl,e)),
        map(binv2int, partition_elements(kl,e)),
        num_rounds, b, e, n_R)))$

/* Input and output as hexadecimal values (the input does not need
   leading zeros, but the output is always padded to 32 hexadecimal
   digits): */
ss_encrypt_hex(p,k,num_rounds,b,e,n_R,n_C) := block([num_hex_digits,num_bits],
  num_bits : e*n_R*n_C,
  num_hex_digits : ceiling(num_bits/4),
  binv2hexstr(
    ss_encrypt_bin(
      rest(
        hexstr2binv(lpad(p,"0",num_hex_digits)),
        (num_hex_digits * 4) - num_bits),
      rest(
        hexstr2binv(lpad(k,"0",num_hex_digits)),
        (num_hex_digits * 4) - num_bits),
      num_rounds, b, e, n_R)))$
ss_decrypt_hex(p,k,num_rounds,b,e,n_R,n_C) := block([num_hex_digits,num_bits],
  num_bits : e*n_R*n_C,
  num_hex_digits : ceiling(num_bits/4),
  binv2hexstr(
    ss_decrypt_bin(
      rest(
        hexstr2binv(lpad(p,"0",num_hex_digits)),
        (num_hex_digits * 4) - num_bits),
      rest(
        hexstr2binv(lpad(k,"0",num_hex_digits)),
        (num_hex_digits * 4) - num_bits),
      num_rounds, b, e, n_R)))$


/*!
\endhtmlonly
*/
