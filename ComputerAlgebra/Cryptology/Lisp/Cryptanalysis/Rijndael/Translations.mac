/* Matthew Gwynne, 26.3.2008 (Swansea) */
/* Copyright 2008, 2009, 2010 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/Translations.mac
  \brief Cryptanalysis functions for translations of Rijndael into active clauses etc

Use by

oklib_load("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/Translations.mac");
  
*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/AdvancedEncryptionStandard.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/SboxAnalysis.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/FieldOperationsAnalysis.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul2CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul3CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul11CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul13CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul14CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul9CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/SboxCNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/FiniteFunctions/TseitinTranslation.mac")$


/* NOTE : 

  In several functions such as aes_round_cp, and aes_key_expansion_cp "blocks"
  are mentioned, which are matrices (4x4 as standard) of bytes (8 bits) boolean
  variables used as a unit of computation within the AES.

  In this translation, currently, when given as a list of variables, each
  block is considered column by column as is standard in the literature.

*/


/* ***************************************
   * Sets of Conditions                  *
   ***************************************
*/

oklib_plain_include(graphs)$

aes_var_ordering : hash_table()$

/* Resets variable numbering back to default: */
aes_reset_vars() := block(
  aes_var_ordering : hash_table(),
  aes_cnf_var_number : 0 
)$

aes_cnf_var_number : 0$

/* Generates a list of integer variables where the values n and m are used to
   determine the range of variables to return (using an offset to ensure new
   variables are returned for each call to this function), where "name" is used
   to ensure certain special variable cases (such as plaintext variables etc)
   are given identifiable variables: */
aes_make_vars_int(name,n,m) :=
  if name = "p" then create_list(i,i,n,m)
  else if name = "k" then create_list(i,i,128 + n,128 + m)
  else if name = "c" then create_list(i,i,256 + n,256 + m)
  else block([rl], 
    rl : create_list(i,i,aes_cnf_var_number + 3 * 128 + n,
      aes_cnf_var_number+3*128+m),
    aes_cnf_var_number : aes_cnf_var_number + (m-n+1),
    return(rl))$

/* Generates a list of variables of the form name_i where
   name is given and i ranges between n and m, offset by a given value to such
   that repeated calls to this function will yield new variables */
aes_make_vars_named(name, n,m) := block(
  [val : get_hash(name,aes_var_ordering), res], 
  if val = false then block(val : 0, set_hash(name,aes_var_ordering, 0)),
  res : create_list(eval_string(concat(name,string(i))),i,val + n, val + m),
  set_hash(name,aes_var_ordering,val+m), return(res))$

/* Allows overriding of variable naming to provide a direct clause set
   translation which seems MUCH quicker than substituting for named variables 
   in the final product within maxima. */
aes_make_vars : aes_make_vars_named;

/* Number of Rounds for the AES */
aes_num_rounds : 10$

/* Returns a set of conditions representing AES given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_cp([bits]) := block([pBits, kBits, cBits, ekBits,ib,ob, rs : {}],
  pBits : take_elements(128,bits),
  kBits : take_elements(128,rest(bits,128)),
  cBits : rest(bits,256),
  ekBits : aes_make_vars("ek",1, (aes_num_rounds+1)*128),
  /* Key Expansion */
  rs : adjoin(apply(aes_key_expansion_c,append(kBits, ekBits)),rs),
  ib : pBits,
  all_ob : aes_make_vars("o",1,(aes_num_rounds)*128),
  ob : take_elements(128,all_ob),
  /* Initial Round Key Addition */
  rs : adjoin(apply(aes_add_c,append(ib,take_elements(128,ekBits),ob)),rs),
  ib : ob,
  /* Rounds */
  for j : 1 thru (aes_num_rounds - 1) do block(
    ob : take_elements(128,rest(all_ob,j*128)),
    rs : adjoin(
      apply(aes_round_c,append(ib,take_elements(128,rest(ekBits,j*128)),ob)),
        rs),
    ib : ob
  ),
  /* Final Round */
  /* If we have a reduced round variant, truncate rather than generalise */
  ob : cBits,
  if aes_num_rounds = 10 then 
    rs : adjoin(apply(aes_final_round_c,append(ib,take_elements(128,
            rest(ekBits,aes_num_rounds*128)),ob)),rs)
  else
    rs : adjoin(apply(aes_round_c,append(ib,take_elements(128,
            rest(ekBits,aes_num_rounds*128)),ob)),rs),
  return(rs)
)$

/* Returns a set of conditions representing AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_round_cp([bits]) := block([iBits, rkBits, oBits,tBits,rs:{}],
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : aes_make_vars("so", 1,128),
  rs : adjoin(apply(aes_subbytes_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : aes_make_vars("ro", 1,128),
  rs : adjoin(apply(aes_shiftrows_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : aes_make_vars("mo", 1,128),
  rs : adjoin(apply(aes_mixcolumns_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : oBits,
  rs : adjoin(apply(aes_add_c,append(iBits, rkBits, tBits)),rs),
  return(rs)
)$

/* Returns a set of conditions representing the final AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_final_round_cp([bits]) := block([iBits, rkBits, oBits,tBits,rs:{}],
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : aes_make_vars("so", 1,128),
  rs : adjoin(apply(aes_subbytes_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : aes_make_vars("ro", 1,128),
  rs : adjoin(apply(aes_shiftrows_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : oBits,
  rs : adjoin(apply(aes_add_c,append(iBits, rkBits, tBits)),rs),
  return(rs)
)$

/* Returns a set of conditions representing the AES Subbytes operation given
   128 plaintext variables and 128 output variables. */
aes_subbytes_cp([bits]) := block([iBits, rkBits, oBits,tBits,rs:{}],
  iBits : take_elements(128,bits),
  oBits : rest(bits,128),
  for i : 0 thru 15 do
    rs : adjoin(apply(aes_sbox_c, 
      append( take_elements(8, rest(iBits,i*8)),
        take_elements(8, rest(oBits,i*8)))
    ),rs),
  return(rs)
)$

/* Returns a set of conditions representing the AES Shiftrows operation given
   128 plaintext variables and 128 output variables. */
aes_shiftrows_cp([bits]) := block([iBits, oBits],
  iBits : take_elements(128,bits),
  oBits : take_elements(128,rest(bits,128)),
  setify(map(lambda([a,b],aes_eq_c(a,b)), oBits, 
    lappend(
      rijn_m2l(aes_shiftrows(rijn_l2m(partition_elements(iBits,8))))))
    )
)$

/* Returns a set of conditions representing the AES Mixcolumns operation given
   128 plaintext variables and 128 output variables. */
aes_mixcolumns_cp([bits]) := block([iBits, oBits, nc],
  iBits : take_elements(128,bits),
  oBits : rest(bits,128),
  nc : length(iBits)/32,
  setify(map(lambda([a,b], 
    apply(aes_mixcolumn_c,append(lappend(a),lappend(b)))),
    partition_elements(partition_elements(iBits,8),nc),
    partition_elements(partition_elements(oBits,8),nc)))
)$

/* Returns a set of conditions representing the AES Mixcolumn operation (the 
   individual operation applied to a single column in AES Mixcolumns) given
   128 plaintext variables and 128 output variables. */
aes_mixcolumn_cp([bits]) := block([iBits, oBits, nc,rs : {}],
  iBits : partition_elements(take_elements(32, bits),8),
  oBits : partition_elements(rest(bits, 32), 8),
  /* First element */
  for i : 0 thru 3 do block([mulAVars, mulBVars, addAVars, addBVars],
    mulAVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul2_c,
        append(iBits[mod(0 + i,4) + 1],mulAVars)), rs),
    mulBVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul3_c,
        append(iBits[mod(1 + i,4) + 1],mulBVars)), rs),
    addAVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, append(mulAVars,mulBVars, addAVars)), rs),
    addBVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, 
      append(iBits[mod(2 + i,4) + 1],iBits[mod(3 + i,4) + 1], addBVars)), rs),
    rs : adjoin(apply(aes_add_c, append(addAVars,addBVars, oBits[i+1])), rs)
  ),
  for i : 0 thru 3 do block(
    [mulAVars, mulBVars, mulCVars, mulDVars, addAVars, addBVars],
    mulAVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul14_c,
        append(oBits[mod(0 + i,4) + 1],mulAVars)), rs),
    mulBVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul11_c,
        append(oBits[mod(1 + i,4) + 1],mulBVars)), rs),
    mulCVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul13_c,
        append(oBits[mod(2 + i,4) + 1],mulCVars)), rs),
    mulDVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul9_c,
        append(oBits[mod(3 + i,4) + 1],mulDVars)), rs),
    addAVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, append(mulAVars,mulBVars, addAVars)), rs),
    addBVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, 
      append(mulCVars,mulDVars, addBVars)), rs),
    rs : adjoin(apply(aes_add_c, append(addAVars,addBVars, iBits[i+1])), rs)
  ),
  return(rs)
)$

/* Generates a set of unit clauses (initially simpler than simplifying constants
   or making partial assignments) to set the given vars (representing the bits
   of AES round constants) to the round constant for round n */
aes_round_constant(ivars, n) := block([rc_el,rc_l],
  rc_el : xreduce(lambda([a,b],aes_field_mul_data[b,a+1]),
               create_list(2,i,1,(n -1)),1),
  rc_l : egf_coeffs(rijn_bit_field,egf_num2poly(rijn_byte_field,rc_el),7),
  setify(map(
    lambda([a,b], if b = 0 then {-a} else {a}),
    ivars, rc_l
  ))
)$

/* Returns a set of conditions representing the AES keyschedule operation given
   128 key variables and 128 * (aes_num_rounds + 1) output variables
   (consecutive lots of 128 bits representing the individual round keys).
   
   The first 128 variables in the list "bits" represent the key block for the 
   cipher, and then the (i+1)'th consecutive 128 variables in "bits" represents
   the round key block for the i'th round. 
   
   */
aes_key_expansion_cp([bits]) := block(
  [n_k : 4,n_b : 4, n_r : aes_num_rounds ,kBits, rkBits, nc,rs : {}, 
  sboxVars, addVars,fMat, kMat],
  /* Primary Variables */
  kBits : take_elements(128,bits),
  rkBits : rest(bits,128),
  fBits : take_elements(128,rkBits),
  /* Split into columns */
  cols :  partition_elements(partition_elements(rkBits,8),4),
  kMat : partition_elements(partition_elements(kBits,8),4),
  fMat : partition_elements(partition_elements(fBits,8),4),
  /* Make first round key equal */
  for j : 1 thru n_k do
    for i : 1 thru 4 do
      rs : adjoin(apply(aes_eq_c,append(kMat[j][i], fMat[j][i])),rs),
  /* Setup output bits */
  for j : n_k thru (n_b * (n_r +1)) - 1 do block(
    if mod(j, n_k) = 0 then block (
      sboxVars : aes_make_vars("ke",1,8),
      rs : adjoin(apply(aes_sbox_c, append(cols[j][2], sboxVars)), rs),
      addVars : aes_make_vars("ke",1,8),
      rs : adjoin(apply(aes_add_c,
        append(cols[j-n_k+1][1],sboxVars,addVars)),rs),
      rcVars : aes_make_vars("rc",1,8),
      rs : union(rs, aes_round_constant(rcVars,floor(j/n_k))),
      rs : adjoin(apply(aes_add_c, 
        append(rcVars,addVars, 
        cols[j+1][1])),rs),
      for i : 1 thru 3 do block(
        sboxVars : aes_make_vars("ke",1,8),
        rs : adjoin(apply(aes_sbox_c, 
          append(cols[j][mod(i+1,4)+1], sboxVars)), rs),
        rs : adjoin(apply(aes_add_c, 
          append(cols[j-n_k+1][i+1],sboxVars, cols[j+1][i+1])),rs)
      )
    )
    else for i : 1 thru 4 do 
      rs : adjoin( apply(aes_add_c,
        append(cols[j][i], cols[j-n_k+1][i],cols[j+1][i])),rs)
  ),
  return(rs)
)$

/* Returns a set of conditions representing addition within GF(2^n) given
   2 * n input variables (n bits to be added to the next n bits)
   and n output variables. */
aes_add_cp([bits]) := block([ nb : floor(length(bits) / 3)],
  xreduce(union, map(lambda([a,b,c], {{-a,-b,-c},{-a,b,c},{a,-b,c},{a,b,-c}}), 
    take_elements(nb,bits),
    take_elements(nb, rest(bits,nb)), rest(bits,nb*2))))$

/* Returns a set of conditions representing equivalence over 2 sets of bits,
   given 2 consecutive lots of n bits to be represented as logically by the set
   output conditions */
aes_eq_cp([bits]) := block([ nb : floor(length(bits) / 2)],
  xreduce(union, map(lambda([a,b], {{-a,b},{a,-b}}), 
    take_elements(nb,bits), rest(bits,nb))))$

/* Generates (and caches) the canonical CNF representation as a formal clause
   set of AES Field muliplication (within the Rijndael GF(2^8) field) by p 
   (given as an integer) with new variables. 
   
   New variables are introduced for each clause in the canonical DNF 
   representation (see dualts_fcl), where the variables are of the form dts(i)
   and i is the rank in the lexiographical ordering of the clauses.

   Caching here simply means that upon the first call to the function with
   argument "p", the result of the computation for that argument is stored 
   in a global variable "aes_mul_ts_CNF[p]" which will then be returned 
   immediately on subsequent calls to "aes_mul_ts_gen" with the same argument 
   to allow for reuse of "aes_mul_ts_gen" without the overhead of 
   recomputation.
 */
aes_mul_ts_CNF : create_list(false,i,1,256)$
aes_mul_ts_gen(p) := 
  if aes_mul_ts_CNF[p+1] = false then block([plus16 : lambda([x],x+16)],
      [mul_dnf : rijnmult_fulldnf_fcs(p)],
    aes_mul_ts_CNF[p+1] : dualts_fcl(
      [listify(mul_dnf[1]),
       sort(
         listify(mul_dnf[2]),
         lambda([a,b], is(
           rank_lex_ksubsets(map(plus16,a),32) < 
             rank_lex_ksubsets(map(plus16,b),32)))
        )]
     ),
     return(aes_mul_ts_CNF[p+1])
  ) else aes_mul_ts_CNF[p+1]$
/* Returns a set of conditions representing multiplication by 02 or "x" in the
   Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
aes_mul2_cp : aes_mul2_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables*/
aes_mul2_pi_cp([bits]) := rename_fcs(FieldMul2CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul2_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(2),append(bits,aes_make_vars("mul2",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul3_cp : aes_mul3_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul3_pi_cp([bits]) := rename_fcs(FieldMul3CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul3_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(3),append(bits,aes_make_vars("mul3",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul14_cp : aes_mul14_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul14_pi_cp([bits]) := rename_fcs(FieldMul14CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul14_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(14),
      append(bits,aes_make_vars("mul14",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul11_cp : aes_mul11_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul11_pi_cp([bits]) := rename_fcs(FieldMul11CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul11_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(11),
      append(bits,aes_make_vars("mul11",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul13_cp : aes_mul13_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul13_pi_cp([bits]) := rename_fcs(FieldMul13CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul13_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(13),
      append(bits,aes_make_vars("mul13",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul9_cp : aes_mul9_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul9_pi_cp([bits]) := rename_fcs(FieldMul9CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul9_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(9),append(bits,aes_make_vars("mul9",1,256)))[2])$

/* Generates (and caches) the canonical CNF representation as a formal clause
   set of AES Sbox operator with new variables. New variables are 
   introduced for each clause in the canonical DNF representation (see
   dualts_fcl), where the new variables are of the form dts(i) and i is the 
   rank in the lexiographical ordering of the clauses.

   Caching here simply means that upon the first call to the function, the 
   result of the computation is stored in a global variable "aes_sbox_ts_CNF"
   which will then be returned immediately on subsequent calls to
   "aes_sbox_ts_gen" to allow for reuse of "aes_sbox_ts_gen" without the
   overhead of recomputation.
 */
aes_sbox_ts_CNF : false;
aes_sbox_ts_gen() := 
  if aes_sbox_ts_CNF = false then block([plus16 : lambda([x],x+16)],
      [sbox_dnf : rijnsbox_fulldnf_fcs()],
    aes_sbox_ts_CNF : dualts_fcl(
      [listify(sbox_dnf[1]),
       sort(
         listify(sbox_dnf[2]),
         lambda([a,b], is(
           rank_lex_ksubsets(map(plus16,a),32) < 
             rank_lex_ksubsets(map(plus16,b),32)))
        )]
     ), 
     return(aes_sbox_ts_CNF)
  ) else aes_sbox_ts_CNF$
/* Default the translation to use the old Sbox translation. */
aes_sbox_cp : aes_sbox_pi_cp$
/* Returns a set of conditions representing the AES Sbox given the 8 input bits
   and 8 output bits */
/* MG : This should ideally take the CNF to be used */
/* Representation using small subsets of prime implicates and no new
   variables */
aes_sbox_pi_cp([bits]) := rename_fcs(Sbox44ICCNF,bits)[2]$
/* Canonical representation using new variables */
aes_sbox_ts_cp([bits]) :=
  setify(
    rename_fcl(aes_sbox_ts_gen(),append(bits,aes_make_vars("sbox",1,256)))[2])$

/* ***************************************
   * Rewrite functions                  *
   ***************************************
*/

oklib_plain_include("opsubst")$

/* Takes a given set of conditions, a condition name, and a function to
   rewrite with and then produces a new set of conditions */
rewrite_condition(con_set, con_name, con_rewrite_f) := block(
  [num_con : 0, total : length(con_set)], 
  setify(uaapply(append,map(
    lambda([b], block(
      if (oklib_monitor and (mod(num_con,100) = 0)) then block(
        print(con_name, " = ", num_con,"/",total)),
      num_con : num_con + 1,
      if not (op(b) = con_name) then [b] 
      else
        block([result],
          result : ev(opsubst(con_rewrite_f,con_name,b),eval),
          if setp(result) then listify(result) else [result]
      ))),
  listify(con_set)
  ))
))$

/* Given a set of conditions, applies all rewrite rules in a typical order that
   would result in a simple AES translation and returns the result condition 
   set (should be a clause set) . */
/* MG : A more general formal rewrite system is obviously needed rather than 
   this strict hard coded method */
rewrite_all(rs) := block(
  rs : rewrite_condition(rs, aes_key_expansion_c,aes_key_expansion_cp),
  rs : rewrite_condition(rs, aes_round_c, aes_round_cp),
  rs : rewrite_condition(rs, aes_final_round_c, aes_final_round_cp),
  rs : rewrite_condition(rs, aes_shiftrows_c, aes_shiftrows_cp),
  rs : rewrite_condition(rs, aes_mixcolumns_c, aes_mixcolumns_cp),
  rs : rewrite_condition(rs, aes_mixcolumn_c, aes_mixcolumn_cp),
  rs : rewrite_condition(rs, aes_subbytes_c, aes_subbytes_cp),
  rs : rewrite_condition(rs, aes_mul2_c, aes_mul2_cp),
  rs : rewrite_condition(rs, aes_mul3_c, aes_mul3_cp),
  rs : rewrite_condition(rs, aes_mul14_c, aes_mul14_cp),
  rs : rewrite_condition(rs, aes_mul11_c, aes_mul11_cp),
  rs : rewrite_condition(rs, aes_mul13_c, aes_mul13_cp),
  rs : rewrite_condition(rs, aes_mul9_c, aes_mul9_cp),
  rs : rewrite_condition(rs, aes_add_c, aes_add_cp),
  rs : rewrite_condition(rs, aes_eq_c, aes_eq_cp),
  rs : rewrite_condition(rs, aes_sbox_c, aes_sbox_cp),
  return(rs)
)$

/* Produces a CNF clause set representing 128-bit AES: */
aes_cnf_cs() := block([rs : {}, aes_make_vars : aes_make_vars_int],
  aes_reset_vars(),
  rs : apply(aes_cp, 
    append(aes_make_vars("p",1,128), aes_make_vars("k",1,128),
      aes_make_vars("c",1,128))),
  rs : rewrite_all(rs),
  return(rs)
)$

/* Produces a formal CNF clause set representing 128-bit AES */
aes_cnf_fcs() := block([aes_cs : {}, aes_make_vars : aes_make_vars_int],
  aes_cs : aes_cnf_cs(),
  [create_list(i,i,1,aes_cnf_var_number+3*128), aes_cs])$
output_aes_cnf(n) :=
  output_fcs(
    sconcat("AES, 128-bit key and block, ",aes_num_rounds,
      " Rounds SAT Translation"),
    aes_cnf_fcs(), n)$
output_aes_cnf_stdname() :=
  output_aes_cnf(sconcat("AES_R",aes_num_rounds,".cnf"))$

/* Takes a hexidecimal string in the notation typically used for Rijndael blocks
   (discussed in [Design of Rijndael] and in CryptoSystems plans) and given the
   variable list (assumed to be in the correct left to right bit ordering)
   generates a PA to the given variables to the given constant.  */
aes_hex2pa(hexStr, variableList) := block(
  setify(map(
    lambda([a,b], if b = 0 then -a else a),
        variableList, 
        lappend(map(lambda([a], 
          egf_coeffs(rijn_bit_field,a,7)),hex2gf2t8l(hexStr)))
  ))
)$



/*************************************************************
 * Constraint template rewrite-based translations            *
 *************************************************************
*/

oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/ConstraintTemplateRewriteRules.mac");
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/ConstraintTemplateRewriteSystem.mac");


/* AES */

/* AES Translations

         Boxes -
             For each box we have the following translation

             aes_small_box - Each box is represented by the smallest known cnf
             aes_ts_box    - Each box is represented by the canonical
                             translation using new variables.

         MixColumns -
             For the translation of the MixColumns component, we may either
             add additional constraints translating the inversion as well
             or simply translate the component in the forward direction.

             aes_mc_forward
             aes_mc_bidirectional

*/

declare(aes_small_box, noun)$
declare(aes_ts_box, noun)$
declare(aes_mc_forward, noun)$
declare(aes_mc_bidrectional, noun)$


/* Constraint rewrite translations */

/* Given the number of rounds, produces a list of constraint templates
   which model AES using standard parameters. */
/* TODO: Add the full version that allows all parameters */
aes_csttl(r, mc_tran) :=
  rewrite_all_csttl(
    [cstt_new("aes_cst",
              generate_aes_constraint_vars(1,3*128,lambda([a],a),"aes_cst"),
              [r],lambda([a],a))],
    if mc_tran = aes_mc_bidirectional then aes_bimc_rewrite_mapping_std
    else aes_rewrite_mapping_std)$
aes_cstt_std_l(r) := aes_csttl(r, aes_mc_forward)$


aes_cstt_vars_l(r, mc_tran) :=
  append(
    generate_aes_constraint_vars(1,3*128,lambda([a],a),"aes_cst"),
    rewrite_all_cstt_vars_l(
      [cstt_new("aes_cst",
        generate_aes_constraint_vars(1,3*128,lambda([a],a),"aes_cst"),
        [r],lambda([a],a))],
      if mc_tran = aes_mc_bidirectional then aes_bimc_rewrite_mapping_std
      else aes_rewrite_mapping_std))$
aes_cstt_vars_std_l(r) := aes_cstt_vars_l(r,aes_mc_forward)$


aes_fcl(r, box_tran, mc_tran) := block(
  [aes_csttl : aes_csttl(r,mc_tran),aes_vars_l, prop_csttl],
  aes_vars_l : aes_cstt_vars_l(r,mc_tran),
  prop_csttl : prop_all_csttl(aes_csttl, aes_vars_l, aes_propagations),
  csttl2fcl(prop_csttl[1],prop_csttl[2],
    if box_tran = aes_small_box then
      aes_pi_translation_mapping
    else
      aes_ts_translation_mapping))$

aes_std_fcl(r) := aes_fcl(r, aes_small_box, aes_ts_box)$

/* Small Scale AES */

ss_csttl(r,ss_num_columns, ss_num_rows, ss_exp, final_round_b, mc_tran) :=
  block([num_block_bits : ss_num_columns * ss_num_rows * ss_exp],
    rewrite_all_csttl(
      [cstt_new("ss_cst",
        generate_aes_constraint_vars(1,3*num_block_bits,
          lambda([a],a),"ss_cst"),
          [r,ss_num_columns, ss_num_rows,2, ss_exp,
           ss_polynomial(2,ss_exp),ss_sbox_matrix(2,ss_exp),
           ss_affine_constant(2,ss_exp),
           ss_mixcolumns_matrix(2,ss_exp,ss_num_rows),final_round_b],
           lambda([a],a))],
    if mc_tran = aes_mc_bidirectional then ss_bimc_rewrite_mapping_std
    else ss_rewrite_mapping_std))$
/*aes_cstt_std_l(r) := aes_csttl(r, aes_mc_forward)$*/

/******************************************************************************
 * Global propagation                                                         *
 ******************************************************************************
*/

aes_propagations : [prop_eq_csttl]$


/******************************************************************************
 * CNF translations                                                           *
 ******************************************************************************
*/

/* AES */

/* Translation mapping, mapping constraint templates to the corresponding
   translation functions where constraints are translated to CNF clause-set
   representations using subsets of the prime implicates. */
aes_pi_translation_mapping : [
  ["aes_add_cst", [aes_add_cst_cl, lambda([[a]],[])]],
  ["const_cst", [aes_const_cst_cl, lambda([[a]],[])]],
  ["aes_mul2_cst", [aes_mul2_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul3_cst", [aes_mul3_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul14_cst", [aes_mul14_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul13_cst", [aes_mul13_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul11_cst", [aes_mul11_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul9_cst", [aes_mul9_pi_cst_cl, lambda([[a]],[])]],
  ["aes_sbox_cst", [aes_sbox_pi_cst_cl, lambda([[a]],[])]]]$

/* Translation mapping, mapping constraint templates to the corresponding
   translation functions where constraints are translated to representations
   using subsets of the prime implicates. */
aes_ts_translation_mapping : [
  ["aes_add_cst", [aes_add_cst_cl, lambda([[a]],[])]],
  ["const_cst", [aes_const_cst_cl, lambda([[a]],[])]],
  ["aes_mul2_cst", [aes_mul2_ts_cst_cl, aes_mul2_ts_var_l]],
  ["aes_mul3_cst", [aes_mul3_ts_cst_cl, aes_mul3_ts_var_l]],
  ["aes_mul14_cst", [aes_mul14_ts_cst_cl, aes_mul14_ts_var_l]],
  ["aes_mul13_cst", [aes_mul13_ts_cst_cl, aes_mul13_ts_var_l]],
  ["aes_mul11_cst", [aes_mul11_ts_cst_cl, aes_mul11_ts_var_l]],
  ["aes_mul9_cst", [aes_mul9_ts_cst_cl, aes_mul9_ts_var_l]],
  ["aes_sbox_cst", [aes_sbox_ts_cst_cl, aes_sbox_ts_var_l]]]$

/* Small Scale */

/* Translation mapping, mapping constraint templates to the corresponding
   translation functions where constraints are translated to CNF clause-set
   representations using subsets of the prime implicates. */
ss_pi_translation_mapping : [
  ["aes_add_cst", [aes_add_cst_cl, lambda([[a]],[])]],
  ["const_cst", [aes_const_cst_cl, lambda([[a]],[])]],
  ["aes_mul2_cst", [aes_mul2_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul3_cst", [aes_mul3_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul14_cst", [aes_mul14_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul13_cst", [aes_mul13_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul11_cst", [aes_mul11_pi_cst_cl, lambda([[a]],[])]],
  ["aes_mul9_cst", [aes_mul9_pi_cst_cl, lambda([[a]],[])]],
  ["aes_sbox_cst", [aes_sbox_pi_cst_cl, lambda([[a]],[])]]]$

/* Translation mapping, mapping constraint templates to the corresponding
   translation functions where constraints are translated to representations
   using subsets of the prime implicates. */
ss_ts_translation_mapping : [
  ["aes_add_cst", [aes_add_cst_cl, lambda([[a]],[])]],
  ["const_cst", [aes_const_cst_cl, lambda([[a]],[])]],
  ["aes_mul2_cst", [aes_mul2_ts_cst_cl, aes_mul2_ts_var_l]],
  ["aes_mul3_cst", [aes_mul3_ts_cst_cl, aes_mul3_ts_var_l]],
  ["aes_mul14_cst", [aes_mul14_ts_cst_cl, aes_mul14_ts_var_l]],
  ["aes_mul13_cst", [aes_mul13_ts_cst_cl, aes_mul13_ts_var_l]],
  ["aes_mul11_cst", [aes_mul11_ts_cst_cl, aes_mul11_ts_var_l]],
  ["aes_mul9_cst", [aes_mul9_ts_cst_cl, aes_mul9_ts_var_l]],
  ["aes_sbox_cst", [aes_sbox_ts_cst_cl, aes_sbox_ts_var_l]]]$

/*!
\endhtmlonly
*/
