/* Matthew Gwynne, 28.9.2010 (Swansea) */
/* Copyright 2010, 2011, 2013 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/ConstraintTemplateRewriteRules.mac
  \brief Functions for translating AES constraint templates into further AES constraint templates and CNF representations.

Use by

oklib_load("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/ConstraintTemplateRewriteRules.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/AdvancedEncryptionStandard.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/ConstraintTemplateRewriteSystem.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/Translations.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/SboxCNF.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/FiniteFunctions/Permutations.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Generators/Generators.mac")$


/*!
  \todo Editorial improvements
  <ul>
   <li> Contract long names like generate_aes_constraint_vars to something like
   trans_aes_gen_vars and use subst to give default block level scoping to turn
   this into something like gv. </li>
   <li> This will allow defaulting of a variety of variables that can then be
   used throughout as the standard variables of that type. </li>
   <li> Such a construction should also take parameters such as the namespace
   and so on and construct all the defaults. </li>
   <li> Expand names like  pBits etc for clarity. </li>
  </ul>
*/


/* ***********************************************
   * Constraint template rewrite translation     *
   ***********************************************
*/

/* The translation system given below is designed to replace the above
   translation system in it's entirety but must be introduced piecewise,
   with appropriate tests. */

/* AES variables - aes_v(i,id) */
/* id here is used as an identifier such as "ks" for key schedule bits, "o"
   for output bits etc. */
/* These are used throughout for all AES constraint templates */
kill(aes_v)$
declare(aes_v, noun)$
declare(aes_v, posfun)$
aes_var(i,id) := nounify(aes_v)(i,id)$

/* Given two integers n and m, a namespace, and
   a noun identifier, returns a list of AES variables of the form
   aes_v(i,id) for n <= i <= m. */
generate_aes_constraint_vars(n,m,namespace, id) :=
  create_list(namespace(aes_var(i,id)),i,n,m)$

aes_rewrite_mapping_std :
  [["aes_cst",aes_cstrb],["aes_key_expansion_cst",aes_key_expansion_cstrb],
  ["aes_round_cst",aes_round_cstrb],
  ["aes_final_round_cst",aes_final_round_cstrb],
  ["aes_subbytes_cst",aes_subbytes_cstrb],
  ["aes_shiftrows_cst",aes_shiftrows_cstrb],
  ["aes_mixcolumns_cst",aes_mixcolumns_cstrb],
  ["aes_mixcolumn_cst",aes_mixcolumn_cstrb]]$

aes_bimc_rewrite_mapping_std :
  [["aes_cst",aes_cstrb],["aes_key_expansion_cst",aes_key_expansion_cstrb],
  ["aes_round_cst",aes_round_cstrb],
  ["aes_final_round_cst",aes_final_round_cstrb],
  ["aes_subbytes_cst",aes_subbytes_cstrb],
  ["aes_shiftrows_cst",aes_shiftrows_cstrb],
  ["aes_mixcolumns_cst",aes_mixcolumns_cstrb],
  ["aes_mixcolumn_cst",aes_bi_mixcolumn_cstrb]]$


/* ***********************************************
   * Constraint template rewrite functions       *
   ***********************************************
*/

/* AES constraint rewrite bundle */
aes_cstrb : [aes_cstr_cstl,aes_namespace,aes_ns_var_l]$

/* Namespace */
kill(aes_ctr_ns)$
declare(aes_ctr_ns,noun)$
declare(aes_ctr_ns,posfun)$
aes_namespace([args]) := apply(nounify(aes_ctr_ns),args)$


/* Returns a list of the variables introduced when
   applying aes_ctr_ctl given the auxiliary parameter
   list arg_l, under the given rewrite_mapping.

   For example, given the constraint template instance

   aes_ct(1,...,384,arg_l)

   is rewritten using aes_ctr_ctl, under
   rewrite_mapping, the list of variables
   returned by this function are exactly those
   variables introduced by aes_ctr_ctl and
   any rewritten-subconstraints it introduces
   (mapped to the corresponding rewrite functions
   by variable mapping, that is, this function
   recursively generates such a variable list,
   based on sub-constraints it uses).
*/
aes_ns_var_l(cst) := block(
  [aes_num_rounds,vars : [],namespace],
  namespace : cstt_namespace_new (aes_namespace,cst),
  aes_num_rounds : cst[3],
  vars : cons(
    generate_aes_constraint_vars(1,(aes_num_rounds+1)*128,
      namespace,nounify(k)),vars),
  vars : cons(
    generate_aes_constraint_vars(1,aes_num_rounds*128,
      namespace,nounify(o)),vars),
  return(lappend(vars)))$
/* Rewrite function designed to rewrite the AES constraint template "aes_ct".

   Takes as arguments 385 variables, which are the arguments of the "aes_ct"
   instance being rewritten.

   The first 384 of these are 128 plaintext, 128 key and 128 ciphertext
   variables respectively. The final argument is a pair (list) of auxiliary
   arguments, the first being the namespace to place all variables introduced
   by this function within, and the second being the integer number of rounds
   which specifies which round-variant of AES is being rewritten.

   The result of this function is a list of constraint template instances
   representing the AES constraint template instance being rewritten.
*/
aes_cstr_cstl(cst) := block(
  [bits, pBits, kBits, cBits, ekBits, ib, ob, rs : [],namespace,
  aes_num_rounds],
  aes_num_rounds : cst[3],
  bits : cst[2],
  namespace : cstt_namespace_new(aes_namespace,cst),
  pBits : take_elements(128,bits),
  kBits : take_elements(128,rest(bits,128)),
  cBits : rest(bits,256),
  ekBits :
    generate_aes_constraint_vars(
      1,(aes_num_rounds+1)*128,namespace,nounify(k)),
  /* Key Expansion */
  rs : cons(
    cstt_new(
      "aes_key_expansion_cst",append(kBits,ekBits),[aes_num_rounds],namespace),
    rs),
  ib : pBits,
  all_ob :
    generate_aes_constraint_vars(1,(aes_num_rounds)*128,namespace,nounify(o)),
  ob : take_elements(128,all_ob),
  /* Initial Round Key Addition */
  rs : cons(
    cstt_new(
      "aes_add_cst",append(ib,take_elements(128,ekBits),ob),[],namespace),
    rs),
  ib : ob,
  /* Rounds */
  for j : 1 thru (aes_num_rounds - 1) do block(
    ob : take_elements(128,rest(all_ob,j*128)),
    rs : cons(
      cstt_new("aes_round_cst",
        append(ib,take_elements(128,rest(ekBits,j*128)),ob),
        [i], namespace),
      rs),
    ib : ob
  ),
  /* Final Round */
  /* If we have a variant with a reduced number of rounds, truncate rather
     than generalise */
  ob : cBits,
  if aes_num_rounds = 10 then 
    rs : cons(
      cstt_new("aes_final_round_cst",
        append(ib,take_elements(128,rest(ekBits,aes_num_rounds*128)),ob),
        [],namespace),
      rs)
  else
    rs : cons(
      cstt_new("aes_round_cst",
        append(ib,take_elements(128,rest(ekBits,aes_num_rounds*128)),ob),
        [aes_num_rounds],namespace),
      rs),
  return(rs)
)$

/*
   AES Round

*/

aes_round_cstrb :
 [aes_round_cstr_cstl, aes_round_namespace, aes_round_ns_var_l]$

/* AES round namespace */
kill(aes_round_ns)$
declare(aes_round_ns,noun)$
declare(aes_round_ns,posfun)$
aes_round_namespace([args]) := apply(nounify(aes_round_ns),args)$

aes_round_ns_var_l(cst) := block([vars : [],namespace],
  namespace : cstt_namespace_new(aes_round_namespace,cst),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(so)),vars),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(ro)),vars),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(mo)),vars),
  return(lappend(vars)))$
/* Returns a set of conditions representing AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_round_cstr_cstl(cst) := block(
  [namespace,bits,iBits, rkBits, oBits,tBits,rs:[]],
  bits : cst[2],
  namespace : cstt_namespace_new(aes_round_namespace,cst),
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(so)),
  rs : cons(
    cstt_new("aes_subbytes_cst",
        append(iBits,tBits),
        [],namespace),
    rs),
  iBits : tBits,
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(ro)),
  rs : cons(
    cstt_new("aes_shiftrows_cst",
        append(iBits,tBits),
        [],namespace),
    rs),
  iBits : tBits,
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(mo)),
  rs : cons(
    cstt_new("aes_mixcolumns_cst",
      append(iBits, tBits),
      [],namespace),
    rs),
  iBits : tBits,
  tBits : oBits,
  rs : cons(
    cstt_new("aes_add_cst",
      append(iBits, rkBits, tBits),
      [],namespace),
    rs),
  return(rs)
)$

/*
   AES Final round
   
*/

aes_final_round_cstrb :
 [aes_final_round_cstr_cstl, aes_final_round_namespace,aes_final_round_ns_var_l]$

/* AES final round namespace */
kill(aes_final_round_ns)$
declare(aes_final_round_ns,noun)$
declare(aes_final_round_ns,posfun)$
aes_final_round_namespace([args]) := apply(nounify(aes_final_round_ns),args)$

aes_final_round_ns_var_l(cst) := block(
  [vars : [],namespace],
  namespace : cstt_namespace_new(aes_final_round_namespace,cst),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(so)),vars),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(ro)),vars),
  return(lappend(vars)))$
/* Returns a set of conditions representing AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_final_round_cstr_cstl(cst) := block(
  [namespace,bits,iBits, rkBits, oBits,tBits,rs:[]],
  bits : cst[2],
  namespace : cstt_namespace_new(aes_final_round_namespace,cst),
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(so)),
  rs : cons(
    cstt_new("aes_subbytes_cst",
        append(iBits,tBits),
        [],namespace),
    rs),
  iBits : tBits,
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(ro)),
  rs : cons(
    cstt_new("aes_shiftrows_cst",
        append(iBits,tBits),
        [],namespace),
    rs),
  iBits : tBits,
  tBits : oBits,
  rs : cons(
    cstt_new("aes_add_cst",
      append(iBits, rkBits, tBits),
      [],namespace),
    rs),
  return(rs)
)$

/*
   Subbytes
   
*/

aes_subbytes_cstrb :
 [aes_subbytes_cstr_cstl,aes_subbytes_namespace,aes_subbytes_ns_var_l]$


/* AES subbytes namespace */
kill(aes_subbytes_ns)$
declare(aes_subbytes_ns,noun)$
declare(aes_subbytes_ns,posfun)$
aes_subbytes_namespace([args]) := apply(nounify(aes_subbytes_ns),args)$

aes_subbytes_ns_var_l([a]) := []$
/* Returns a set of conditions representing the AES Subbytes operation given
   128 plaintext variables and 128 output variables. */
aes_subbytes_cstr_cstl(cst) := block(
  [namespace,bits,iBits, rkBits, oBits,tBits,rs:[]],
  bits : cst[2],
  namespace : cstt_namespace_new(aes_subbytes_namespace,cst),
  iBits : take_elements(128,bits),
  oBits : rest(bits,128),
  for i : 0 thru 15 do
    rs : cons(
      cstt_new("aes_sbox_cst",
        append(
          take_elements(8, rest(iBits,i*8)),
          take_elements(8, rest(oBits,i*8))),
        [i], namespace),
      rs),
  return(rs)
)$


/*
   Shiftrows

*/

aes_shiftrows_cstrb :
 [aes_shiftrows_cstr_cstl,aes_shiftrows_namespace,aes_shiftrows_ns_var_l]$

/* AES shiftrows namespace */
kill(aes_shiftrows_ns)$
declare(aes_shiftrows_ns,noun)$
declare(aes_shiftrows_ns,posfun)$
aes_shiftrows_namespace(arg_l) := apply(nounify(aes_shiftrows_ns),arg_l)$


aes_shiftrows_ns_var_l([a]) := []$
/* Returns a set of conditions representing the AES Shiftrows operation given
   128 plaintext variables and 128 output variables. */
aes_shiftrows_cstr_cstl(cst) := block(
  [namespace,bits,iBits, oBits,count : 0],
  bits : cst[2],
  namespace : cstt_namespace_new(aes_shiftrows_namespace,cst),
  iBits : take_elements(128,bits),
  oBits : take_elements(128,rest(bits,128)),
  map(
    lambda([a,b],
      cstt_new("eq_cst",[a,b],
        [],namespace)), oBits,
    lappend(
      rijn_m2l(aes_shiftrows(rijn_l2m(partition_elements(iBits,8))))))
)$


/*
   Mixcolumns

*/

aes_mixcolumns_cstrb :
 [aes_mixcolumns_cstr_cstl,aes_mixcolumns_namespace,aes_mixcolumns_ns_var_l]$

/* AES mixcolumns namespace */
kill(aes_mixcolumns_ns)$
declare(aes_mixcolumns_ns,noun)$
declare(aes_mixcolumns_ns,posfun)$
aes_mixcolumns_namespace([arg_l]) := apply(nounify(aes_mixcolumns_ns),arg_l)$


aes_mixcolumns_ns_var_l([a]) := []$
/* Returns a set of conditions representing the AES Mixcolumns operation given
   128 plaintext variables and 128 output variables. */
aes_mixcolumns_cstr_cstl(cst) := block(
  [namespace, bits,iBits, oBits, nc, mc_i : 0],
  bits : cst[2],
  namespace : cstt_namespace_new(aes_mixcolumns_namespace,cst),
  iBits : take_elements(128,bits),
  oBits : rest(bits,128),
  nc : length(iBits)/32,
  map(lambda([a,b,column], 
    cstt_new("aes_mixcolumn_cst",
      append(lappend(a),lappend(b)),
      [mc_i : mc_i + 1],namespace)),
    partition_elements(partition_elements(iBits,8),nc),
    partition_elements(partition_elements(oBits,8),nc),
    create_list(i,i,1,4))
)$


/*
   Bidirectional Mixcolumn

*/

aes_bi_mixcolumn_cstrb :
 [aes_bi_mixcolumn_cstr_cstl,aes_bi_mixcolumn_namespace,aes_bi_mixcolumn_ns_var_l]$


/* AES bi_mixcolumn namespace */
kill(aes_bi_mixcolumn_ns)$
declare(aes_bi_mixcolumn_ns,noun)$
declare(aes_bi_mixcolumn_ns,posfun)$
aes_bi_mixcolumn_namespace([args]) := apply(nounify(aes_bi_mixcolumn_ns),args)$

aes_bi_mixcolumn_ns_var_l(cstt) := block(
  [cstt_new : cstt_namespace_replace(cstt,
    cstt_namespace_new(aes_bi_mixcolumn_namespace,cstt))],
  append(
    aes_mixcolumn_ns_var_l(cstt_new),
    aes_inv_mixcolumn_ns_var_l(cstt_new)))$
/* Returns a set of conditions representing the AES bidirectional
   mixcolumn operation (the individual operation applied to a single column in
   AES bi_mixcolumns) given 128 plaintext variables and 128 output variables.
 */
aes_bi_mixcolumn_cstr_cstl(cstt) := block(
  [cstt_new : cstt_namespace_replace(cstt,
    cstt_namespace_new(aes_bi_mixcolumn_namespace,cstt))],
  append(
    aes_mixcolumn_cstr_cstl(cstt_new),
    aes_inv_mixcolumn_cstr_cstl(cstt_new)))$


/*
   Mixcolumn

*/

aes_mixcolumn_cstrb :
 [aes_mixcolumn_cstr_cstl,aes_mixcolumn_namespace,aes_mixcolumn_ns_var_l]$


/* AES mixcolumn namespace */
kill(aes_mixcolumn_ns)$
declare(aes_mixcolumn_ns,noun)$
declare(aes_mixcolumn_ns,posfun)$
aes_mixcolumn_namespace([args]) := apply(nounify(aes_mixcolumn_ns),args)$

aes_mixcolumn_ns_var_l(cst) := block([vars : [],namespace],
  namespace :
    cstt_namespace_new(aes_mixcolumn_namespace,cst),
  vars : cons(
      generate_aes_constraint_vars(1,32,namespace,nounify(mc2)),vars),
  vars : cons(
      generate_aes_constraint_vars(1,32,namespace,nounify(mc3)),vars),
  return(lappend(vars)))$
/* Returns a set of conditions representing the AES Mixcolumn operation (the 
   individual operation applied to a single column in AES Mixcolumns) given
   128 plaintext variables and 128 output variables. */
aes_mixcolumn_cstr_cstl(cst) := block(
  [namespace,bits,iBits, oBits, nc,rs : []],
  bits : cst[2],
  namespace :
    cstt_namespace_new(aes_mixcolumn_namespace,cst),
  iBits : partition_elements(take_elements(32, bits),8),
  oBits : partition_elements(rest(bits, 32), 8),
  /* First element */
  for i : 0 thru 3 do block([mulAVars, mulBVars, addAVars, addBVars],
    mulAVars :
      generate_aes_constraint_vars(8*i + 1,8*i + 8,namespace,nounify(mc2)),
    rs : cons(
      cstt_new("aes_mul2_cst",
        append(iBits[mod(0 + i,4) + 1],mulAVars),
        [i],namespace),
      rs),
    mulBVars :
      generate_aes_constraint_vars(8*i + 1,8*i + 8,namespace,nounify(mc3)),
    rs : cons(
      cstt_new("aes_mul3_cst",
        append(iBits[mod(1 + i,4) + 1],mulBVars),
        [i],namespace),
      rs),
    rs : cons(
      cstt_new("aes_add_cst",
        append(
          mulAVars,mulBVars,iBits[mod(2 + i,4) + 1],
          iBits[mod(3 + i,4) + 1],oBits[i+1]),[4], namespace),
      rs)
  ),
  return(rs)
)$


/*
   Inverse Mixcolumn

*/

aes_inv_mixcolumn_cstrb :
 [aes_inv_mixcolumn_cstr_cstl,aes_inv_mixcolumn_namespace,aes_inv_mixcolumn_ns_var_l]$


/* AES inv_mixcolumn namespace */
kill(aes_inv_mixcolumn_ns)$
declare(aes_inv_mixcolumn_ns,noun)$
declare(aes_inv_mixcolumn_ns,posfun)$
aes_inv_mixcolumn_namespace([args]) := apply(nounify(aes_inv_mixcolumn_ns),args)$

aes_inv_mixcolumn_ns_var_l(cst) := block([vars : [],namespace],
  namespace :
    cstt_namespace_new(aes_inv_mixcolumn_namespace,cst),
  vars : cons(
      generate_aes_constraint_vars(1,32,namespace,nounify(mc14)),vars),
  vars : cons(
      generate_aes_constraint_vars(1,32,namespace,nounify(mc11)),vars),
  vars : cons(
      generate_aes_constraint_vars(1,32,namespace,nounify(mc13)),vars),
  vars : cons(
      generate_aes_constraint_vars(1,32,namespace,nounify(mc9)),vars),
  return(lappend(vars)))$
/* Returns a set of conditions representing the AES Inverse Mixcolumn operation (the 
   individual operation applied to a single column in AES Inverse Mixcolumns) given
   128 plaintext variables and 128 output variables. */
aes_inv_mixcolumn_cstr_cstl(cst) := block(
  [namespace,bits,iBits, oBits, nc,rs : []],
  bits : cst[2],
  namespace :
    cstt_namespace_new(aes_inv_mixcolumn_namespace,cst),
  iBits : partition_elements(take_elements(32, bits),8),
  oBits : partition_elements(rest(bits, 32), 8),
  /* Perform matrix multiplication */
  for i : 0 thru 3 do block(
    [mulAVars, mulBVars, mulCVars, mulDVars, addAVars, addBVars],
    mulAVars :
      generate_aes_constraint_vars(8*i+1,8*i+8,namespace,nounify(mc14)),
    rs : cons(
      cstt_new("aes_mul14_cst",
        append(oBits[mod(0 + i,4) + 1],mulAVars),
        [i],namespace),
      rs),
    mulBVars :
      generate_aes_constraint_vars(8*i+1,8*i+8,namespace,nounify(mc11)),
    rs : cons(
      cstt_new("aes_mul11_cst",
        append(oBits[mod(1 + i,4) + 1],mulBVars),
        [i],namespace),
      rs),
    mulCVars :
      generate_aes_constraint_vars(8*i+1,8*i+8,namespace,nounify(mc13)),
    rs : cons(
      cstt_new("aes_mul13_cst",
        append(oBits[mod(2 + i,4) + 1],mulCVars),
        [i],namespace),
      rs),
    mulDVars :
      generate_aes_constraint_vars(8*i+1,8*i+8,namespace,nounify(mc9)),
    rs : cons(
      cstt_new("aes_mul9_cst",
        append(oBits[mod(3 + i,4) + 1],mulDVars),
        [i],namespace),
      rs),
    rs : cons(
      cstt_new("aes_add_cst",
        append(mulAVars, mulBVars, mulCVars, mulDVars, iBits[i+1]),
        [4],namespace),
      rs)
  ),
  return(rs)
)$


/*
   Key expansion

*/


aes_key_expansion_cstrb :
 [aes_key_expansion_cstr_cstl,aes_key_expansion_namespace,
  aes_key_expansion_ns_var_l]$

/* Generates a set of unit-clauses (initially simpler than simplifying constants
   or making partial assignments) to set the given vars (representing the bits
   of AES round constants) to the round constant for round n */
aes_round_constant_l(n) := block([rc_el,rc_l],
  rc_el : xreduce(lambda([a,b],aes_field_mul_data[b,a+1]),
               create_list(2,i,1,(n -1)),1),
  rc_l : egf_coeffs(rijn_bit_field,egf_num2poly(rijn_byte_field,rc_el),7),
  return(rc_l)
)$

/* AES key expansion namespace */
kill(aes_key_expansion_ns)$
declare(aes_key_expansion_ns,noun)$
declare(aes_key_expansion_ns,posfun)$
aes_key_expansion_namespace([args]) :=
  apply(nounify(aes_key_expansion_ns),args)$

aes_key_expansion_ns_var_l(cst) := block(
  [n_r : cst[3],vars : [],namespace],
  namespace : cstt_namespace_new(aes_key_expansion_namespace,cst),
  vars : cons(
    generate_aes_constraint_vars(1,4*8*n_r,namespace,nounify(sb)),vars),
  vars : cons(
    generate_aes_constraint_vars(1,8*n_r,namespace,nounify(ke)),vars),
  vars : cons(
    generate_aes_constraint_vars(1,8*n_r,namespace,nounify(rc)),vars),
  return(lappend(vars)))$
/* Returns a set of conditions representing the AES keyschedule operation given
   128 key variables and 128 * (aes_num_rounds + 1) output variables
   (consecutive lots of 128 bits representing the individual round keys).
   
   The first 128 variables in the list "bits" represent the key block for the 
   cipher, and then the (i+1)'th consecutive 128 variables in "bits" represents
   the round key block for the i'th round. 
   
   */
aes_key_expansion_cstr_cstl(cst) := block(
  [namespace,bits, n_k : 4,n_b : 4, n_r,kBits, rkBits, nc,rs : [],
   sboxVars, addVars,fMat, kMat, sbox_i : 0, rc_i : 0],
  bits : cst[2],
  namespace : cstt_namespace_new(aes_key_expansion_namespace,cst),
  n_r : cst[3],
  /* Primary Variables */
  kBits : take_elements(128,bits),
  rkBits : rest(bits,128),
  fBits : take_elements(128,rkBits),
  /* Split into columns */
  cols :  partition_elements(partition_elements(rkBits,8),4),
  kMat : partition_elements(partition_elements(kBits,8),4),
  fMat : partition_elements(partition_elements(fBits,8),4),
  /* Make first round key equal */
  for j : 1 thru n_k do
    for i : 1 thru n_b do
      rs : cons(
        cstt_new("eq_cst",
          append(kMat[j][i], fMat[j][i]),
          [],namespace),
        rs),
  /* Setup output bits */
  for j : n_k thru (n_b * (n_r +1)) - 1 do block(
    if mod(j, n_k) = 0 then block (
      sboxVars : generate_aes_constraint_vars(
        (j-n_k)*8*(4/n_k) +1,(j-n_k)*8*(4/n_k) +8,namespace,nounify(sb)),
      rs : cons(
        cstt_new("aes_sbox_cst",
          append(cols[j][2], sboxVars),
          [sbox_i : sbox_i + 1],namespace),
        rs),
      addVars : generate_aes_constraint_vars(
        (j-n_k)*(8/n_k) +1,(j-n_k)*(8/n_k) +8,namespace,nounify(ke)),
      rs : cons(
        cstt_new("aes_add_cst",
          append(cols[j-n_k+1][1],sboxVars,addVars),
          [],namespace),
        rs),
      rcVars : generate_aes_constraint_vars(
        (j-n_k)*(8/n_k) + 1,(j-n_k)*(8/n_k) + 8,namespace,nounify(rc)),
      rs : cons(
        cstt_new("const_cst",
            rcVars,
            cons(rc_i : rc_i + 1,aes_round_constant_l(floor(j/n_k))),
            namespace),
        rs),
      rs : cons(
        cstt_new("aes_add_cst", 
          append(rcVars,addVars, cols[j+1][1]),
          [],namespace),
        rs),
      for i : 1 thru 3 do block([offset: ((j-n_k)*(4/n_k) + i)*8],
        sboxVars : generate_aes_constraint_vars(
          offset + 1,offset + 8,namespace,nounify(sb)),
        rs : cons(
          cstt_new("aes_sbox_cst", 
            append(cols[j][mod(i+1,4)+1], sboxVars),
            [sbox_i : sbox_i + 1],namespace),
          rs),
        rs : cons(
          cstt_new("aes_add_cst", 
            append(cols[j-n_k+1][i+1],sboxVars, cols[j+1][i+1]),
            [],namespace),
          rs)
      )
    )
    else for i : 1 thru 4 do 
      rs : cons(
        cstt_new("aes_add_cst",
          append(cols[j][i], cols[j-n_k+1][i],cols[j+1][i]),
          [],namespace),
        rs)
  ),
  return(rs)
)$


/* ****************************************
   * CNF Translation functions            *
   ****************************************
*/

/* Returns a list of CNF clauses representing addition within GF(2^n) given
   an "aes_add_cst" constraint with m * n input variables (n lots of
   m bits to be summed) and output variables where n is the first
   argument to the constraint. */
aes_add_cst_cl(cst) := block(
  [nb, n : if cstt_args_l(cst) = [] then 2 else cstt_args_l(cst)[1]],
  nb : floor(length(cst[2]) / (n+1)),
  parity_f : lambda([[a]], fcl2cl(fullxoreqs_wv_fcl(1,length(a)-1,a))),
  lappend(
    apply(map,
      append([parity_f],
        partition_elements(cst[2],nb)))))$

/* Returns a list of CNF clauses representing equivalence between the first
   half of the list of variables and the second half in the constraint.

   Variables in the second half may also be "true", in which case, unit
   clauses will be introduced.
*/
aes_eq_cst_cl(cst) := block([ nb : floor(length(cst[2]) / 2)],
  lappend(
    map(lambda([a,b], if b = true then [{a}] else [{-a,b},{a,-b}]), 
    take_elements(nb,cst[2]), rest(cst[2],nb))))$

/* Constants */
aes_const_cst_cl(cst) := block(
  map(lambda([a,b], if b = 1 then {a} else {-a}), cst[2],
    rest(cstt_args_l(cst))))$

/* Returns a list of CNF clauses representing the sbox relation
   given the appropriate sbox constraint ("aes_sbox_cst" with 16 variables
   in the constraint).

   The clauses returned are those originally produced manually through
   experimentation into finding small Sbox representations (see plans
   and SboxMinCNF).
*/
aes_sbox_pi_cst_cl(cst) := rename_fcl(map(listify,SboxMinCNF),cst[2])[2]$


/* AES sbox namespace */
kill(aes_sbox_ts_ns)$
declare(aes_sbox_ts_ns,noun)$
declare(aes_sbox_ts_ns,posfun)$
aes_sbox_ts_namespace([args]) :=
  apply(nounify(aes_sbox_ts_ns),args)$

/* Given an "aes_sbox_cst" constraint, returns the new variables
   introduced by "aes_sbox_ts_cst_cl" when given this constraint. */
aes_sbox_ts_var_l(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_sbox_ts_namespace,cst),
  generate_aes_constraint_vars(1,256, namespace,nounify(sbox_ts)))$

/* Given an "aes_sbox_cst" constraint, returns a clause list
   generated by taking the canonical translation from DNF to CNF
   of the AES Sbox.
*/
aes_sbox_ts_cst_cl(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_sbox_ts_namespace,cst),
    sbox : aes_sbox_ts_gen(),
    vars : append(bits,
      generate_aes_constraint_vars(1,256, namespace,nounify(sbox_ts))),
    cnf : rename_fcl(sbox,vars),
    cnf[2])$

/* Returns a list of CNF clauses representing the multiplication by 02
   given the appropriate constraint ("aes_mul2_cst" with 16 variables
   in the constraint).

   The clauses returned are those originally produced manually through
   experimentation into finding small Sbox representations (see plans
   and FieldMul2CNF).
*/
aes_mul2_pi_cst_cl(cst) := rename_fcl(map(listify,FieldMul2CNF),cst[2])[2]$

/* AES mul2 namespace */
kill(aes_mul2_ts_ns)$
declare(aes_mul2_ts_ns,noun)$
declare(aes_mul2_ts_ns,posfun)$
aes_mul2_ts_namespace([args]) :=
  apply(nounify(aes_mul2_ts_ns),args)$

/* Given an "aes_mul2_cst" constraint, returns the new variables
   introduced by "aes_mul2_ts_cst_cl" when given this constraint. */
aes_mul2_ts_var_l(cst) := block([bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul2_ts_namespace,cst),
  generate_aes_constraint_vars(1,256, namespace,nounify(mul2_ts)))$

/* Returns a list of CNF clauses representing the multiplication by 02
   given the appropriate constraint ("aes_mul2_cst" with 16 variables
   in the constraint).

   The clauses returned are those given by the canonical translation
   using new variables. */
aes_mul2_ts_cst_cl(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul2_ts_namespace,cst),
    rename_fcl(aes_mul_ts_gen(2),
      append(bits,
        generate_aes_constraint_vars(1,256, namespace,nounify(mul2_ts))))[2])$

/* Returns a list of CNF clauses representing the multiplication by 03
   given the appropriate constraint ("aes_mul3_cst" with 16 variables
   in the constraint).

   The clauses returned are those originally produced manually through
   experimentation into finding small Sbox representations (see plans
   and FieldMul3CNF).
*/
aes_mul3_pi_cst_cl(cst) := rename_fcl(map(listify,FieldMul3CNF),cst[2])[2]$

/* AES mul3 namespace */
kill(aes_mul3_ts_ns)$
declare(aes_mul3_ts_ns,noun)$
declare(aes_mul3_ts_ns,posfun)$
aes_mul3_ts_namespace([args]) :=
  apply(nounify(aes_mul3_ts_ns),args)$

/* Given an "aes_sbox_cst" constraint, returns the new variables
   introduced by "aes_sbox_ts_cst_cl" when given this constraint. */
aes_mul3_ts_var_l(cst) := block([bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul3_ts_namespace,cst),
  generate_aes_constraint_vars(1,256, namespace,nounify(mul3_ts)))$

/* Returns a list of CNF clauses representing the multiplication by 03
   given the appropriate constraint ("aes_mul3_cst" with 16 variables
   in the constraint).

   The clauses returned are those given by the canonical translation
   using new variables. */
aes_mul3_ts_cst_cl(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul3_ts_namespace,cst),
    rename_fcl(aes_mul_ts_gen(3),
      append(bits,
        generate_aes_constraint_vars(1,256, namespace,nounify(mul3_ts))))[2])$


/* Returns a list of CNF clauses representing the multiplication by 14
   given the appropriate constraint ("aes_mul14_cst" with 16 variables
   in the constraint).

   The clauses returned are those originally produced manually through
   experimentation into finding small Sbox representations (see plans
   and FieldMul14CNF).
*/
aes_mul14_pi_cst_cl(cst) := rename_fcl(map(listify,FieldMul14CNF),cst[2])[2]$

/* AES mul14 namespace */
kill(aes_mul14_ts_ns)$
declare(aes_mul14_ts_ns,noun)$
declare(aes_mul14_ts_ns,posfun)$
aes_mul14_ts_namespace([args]) :=
  apply(nounify(aes_mul14_ts_ns),args)$

/* Given an "aes_sbox_cst" constraint, returns the new variables
   introduced by "aes_sbox_ts_cst_cl" when given this constraint. */
aes_mul14_ts_var_l(cst) := block([bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul14_ts_namespace,cst),
  generate_aes_constraint_vars(1,256, namespace,nounify(mul14_ts)))$

/* Returns a list of CNF clauses representing the multiplication by 14
   given the appropriate constraint ("aes_mul14_cst" with 16 variables
   in the constraint).

   The clauses returned are those given by the canonical translation
   using new variables. */
aes_mul14_ts_cst_cl(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul14_ts_namespace,cst),
    rename_fcl(aes_mul_ts_gen(14),
      append(bits,
        generate_aes_constraint_vars(1,256, namespace,nounify(mul14_ts))))[2])$

/* Returns a list of CNF clauses representing the multiplication by 11
   given the appropriate constraint ("aes_mul11_cst" with 16 variables
   in the constraint).

   The clauses returned are those originally produced manually through
   experimentation into finding small Sbox representations (see plans
   and FieldMul11CNF).
*/
aes_mul11_pi_cst_cl(cst) := rename_fcl(map(listify,FieldMul11CNF),cst[2])[2]$

/* AES mul11 namespace */
kill(aes_mul11_ts_ns)$
declare(aes_mul11_ts_ns,noun)$
declare(aes_mul11_ts_ns,posfun)$
aes_mul11_ts_namespace([args]) :=
  apply(nounify(aes_mul11_ts_ns),args)$

/* Given an "aes_sbox_cst" constraint, returns the new variables
   introduced by "aes_sbox_ts_cst_cl" when given this constraint. */
aes_mul11_ts_var_l(cst) := block([bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul11_ts_namespace,cst),
  generate_aes_constraint_vars(1,256, namespace,nounify(mul11_ts)))$

/* Returns a list of CNF clauses representing the multiplication by 11
   given the appropriate constraint ("aes_mul11_cst" with 16 variables
   in the constraint).

   The clauses returned are those given by the canonical translation
   using new variables. */
aes_mul11_ts_cst_cl(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul11_ts_namespace,cst),
    rename_fcl(aes_mul_ts_gen(11),
      append(bits,
        generate_aes_constraint_vars(1,256, namespace,nounify(mul11_ts))))[2])$

/* Returns a list of CNF clauses representing the multiplication by 13
   given the appropriate constraint ("aes_mul13_cst" with 16 variables
   in the constraint).

   The clauses returned are those originally produced manually through
   experimentation into finding small Sbox representations (see plans
   and FieldMul13CNF).
*/
aes_mul13_pi_cst_cl(cst) := rename_fcl(map(listify,FieldMul13CNF),cst[2])[2]$

/* AES mul13 namespace */
kill(aes_mul13_ts_ns)$
declare(aes_mul13_ts_ns,noun)$
declare(aes_mul13_ts_ns,posfun)$
aes_mul13_ts_namespace([args]) :=
  apply(nounify(aes_mul13_ts_ns),args)$

/* Given an "aes_sbox_cst" constraint, returns the new variables
   introduced by "aes_sbox_ts_cst_cl" when given this constraint. */
aes_mul13_ts_var_l(cst) := block([bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul13_ts_namespace,cst),
  generate_aes_constraint_vars(1,256, namespace,nounify(mul13_ts)))$

/* Returns a list of CNF clauses representing the multiplication by 13
   given the appropriate constraint ("aes_mul13_cst" with 16 variables
   in the constraint).

   The clauses returned are those given by the canonical translation
   using new variables. */
aes_mul13_ts_cst_cl(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul13_ts_namespace,cst),
    rename_fcl(aes_mul_ts_gen(13),
      append(bits,
        generate_aes_constraint_vars(1,256, namespace,nounify(mul13_ts))))[2])$

/* Returns a list of CNF clauses representing the multiplication by 09
   given the appropriate constraint ("aes_mul9_cst" with 16 variables
   in the constraint).

   The clauses returned are those originally produced manually through
   experimentation into finding small Sbox representations (see plans
   and FieldMul9CNF).
*/
aes_mul9_pi_cst_cl(cst) := rename_fcl(map(listify,FieldMul9CNF),cst[2])[2]$

/* AES mul9 namespace */
kill(aes_mul9_ts_ns)$
declare(aes_mul9_ts_ns,noun)$
declare(aes_mul9_ts_ns,posfun)$
aes_mul9_ts_namespace([args]) :=
  apply(nounify(aes_mul9_ts_ns),args)$

/* Given an "aes_sbox_cst" constraint, returns the new variables
   introduced by "aes_sbox_ts_cst_cl" when given this constraint. */
aes_mul9_ts_var_l(cst) := block([bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul9_ts_namespace,cst),
  generate_aes_constraint_vars(1,256, namespace,nounify(mul9_ts)))$

/* Returns a list of CNF clauses representing the multiplication by 09
   given the appropriate constraint ("aes_mul9_cst" with 16 variables
   in the constraint).

   The clauses returned are those given by the canonical translation
   using new variables. */
aes_mul9_ts_cst_cl(cst) := block(
  [bits : cst[2],namespace],
  namespace : cstt_namespace_new(aes_mul9_ts_namespace,cst),
    rename_fcl(aes_mul_ts_gen(9),
      append(bits,
        generate_aes_constraint_vars(1,256, namespace,nounify(mul9_ts))))[2])$

/*!
\endhtmlonly
*/
