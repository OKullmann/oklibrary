/* Oliver Kullmann, 14.12.2007 (Swansea) */
/* Copyright 2007, 2008, 2009, 2010, 2011, 2012, 2013, 2016 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Satisfiability/Lisp/Resolution/Basics.mac
  \brief Functions for resolution operations on clause-sets

Use by

oklib_load("OKlib/ComputerAlgebra/Satisfiability/Lisp/Resolution/Basics.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/BasicOperations.mac")$
oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/SetSystems.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/HashMaps.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Reductions/UnitClausePropagation.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/PartialAssignments.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/Constructions.mac")$


/* ***************************
   * Single resolution steps *
   ***************************
*/

/* Checks */

/* Checks whether two clauses are resolvable: */
resolvable(C,D) := singletonp(intersection(C, comp_sl(D)))$

/* Checks whether two clauses are resolvable and that the resolvent subsumes
   the two parents: */
two_subsumption_resolvable(C,D) := resolvable(C,D) and is(length(symmdifference(C,D)) = 2)$


/* Basic resolution operations */

/* The resolution literal of two resolvable clauses: */
resolution_literal(C,D) := single_element(intersection(C, comp_sl(D)))$

/* The resolvent of two resolvable clauses: */
resolvent(C,D) := block([x : resolution_literal(C,D)], union(disjoin(x, C), disjoin(-x, D)))$


/* Resolution operations as partial operations, returning either [R]  or []. */

/* If clauses C,D are resolvable with resolution literal x, then return [x],
   else return []: */
resolvable_p(C,D) :=  block([clashs : intersection(C, comp_sl(D))],
 if length(clashs) = 1 then [single_element(clashs)] else [])$

/* If C,D are 2-subsumption-resolvable, return [R], else []: */
two_subsumption_resolvent_p(C,D) := block([x : resolvable_p(C,D)],
 if not emptyp(x) then
  block([x : x[1], R],
   R : disjoin(x,C), if R = disjoin(-x,D) then return([R]) else return([]))
 else return([]))$
/* two_subsumption_resolvent_p(C,D) =
 if two_subsumption_resolvable(C,D) then [resolvent(C,D)] else [] */


/* The variations for sets of parent clauses */

/* Checks whether a set of clauses is resolvable: */
resolvable_s(S) := setp(S) and length(S)=2 and
 block([L : listify(S)], resolvable(L[1], L[2]))$

/* The resolvent of a resolvable set of clauses: */
resolvent_s(S) := block([L : listify(S)], resolvent(L[1], L[2]))$


/* The variation for a given resolution literal */

/* Checks whether two clauses are resolvable on a given resolution literal: */
resolvable_l(C,D,x) := is(intersection(C, comp_sl(D)) = {x})$

/* Checks whether a set of clauses is resolvable on a given resolution literal: */
resolvable_s_l(S,x) := block([L : listify(S)], resolvable_l(L[1], L[2], x) or
 resolvable_l(L[1], L[2], -x))$

/* The resolvent of two resolvable clauses on a given resolution literal: */
resolvent_l(C,D,x) := union(disjoin(x, C), disjoin(-x, D))$

/* The resolvent of a resolvable set of clauses on a given resolution literal:
*/
resolvent_s_l(S,x) := block([L : listify(S)], setdifference(union(L[1], L[2]), {x,-x}))$


/* *************************
   * Adding all resolvents *
   *************************
*/

/* The resolvable 2-sets of a clause-set: */
resolvable_cs(F) := block([LF : l2array(listify(F)), l : length(F), R : []],
 for i : 1 thru l-1 do block([C : LF[i]],
  for j : i+1 thru l do
   if resolvable(C, LF[j]) then R : cons({C,LF[j]}, R)),
 return(setify(R)))$
/* resolvable_cs(F) = subset(powerset(F,2), resolvable_s) */

/* The 2-subsumption-resolvable 2-sets of a clause-set: */
two_subsumption_resolvable_cs(F) := block(
 [LF : l2array(listify(F)), l : length(F), R : []],
 for i : 1 thru l-1 do block([C : LF[i]],
  for j : i+1 thru l do
   if two_subsumption_resolvable(C, LF[j]) then R : cons({C,LF[j]}, R)),
 return(setify(R)))$

/* All resolvents of a clause-set: */
resolvents_cs(F) := map(resolvent_s, resolvable_cs(F))$

/* All 2-subsumption-resolvents of a clause-set: */
two_subsumption_resolvents_cs(F) := map(resolvent_s, two_subsumption_resolvable_cs(F))$

/* Returns a pair [res, R], where res is the set of 2-subsumption-resolvents
   of F, while R is the set of remaining clauses of F, i.e., clauses not
   subsumed by their 2-subsumption-resolvents: */
two_subsumption_resolvents_rem_cs(F) := block(
 [L : l2array(listify(F)), l : length(F), subsumed, result : {}],
  subsumed : okl_make_array(fixnum, l),
  for i : 1 thru l - 1 do block([C : L[i], used : false],
    for j : i+1 thru l do block([R : two_subsumption_resolvent_p(C,L[j])],
      if not emptyp(R) then (
        result : adjoin(R[1], result),
        used : true, subsumed[j] : 1)),
    if used then subsumed[i] : 1),
  return([result,setify(sublist_indicator(array2l(L),subsumed))]))$

/* Adds all resolvents to a clause-set: */
add_resolvents_cs(F) := union(F, resolvents_cs(F))$



/* ***********************
   * Iterated resolution *
   ***********************
*/

monitorcheck_resolution_closure_init(name) := if oklib_monitor then (
  print(sconcat("M[",name,"]:"), history))$
monitorcheck_resolution_closure_loop(name) := if oklib_monitor then (
  print(last(history)))$


/* The (full) resolution closure of a clause-set, returning a pair, where
   the second element is the list of clause-counts for the stages: */
resolution_closure_cs(F) := block(
 [nF : add_resolvents_cs(F), history : [length(F)]],
  history : append(history, [length(nF)]),
  monitorcheck_resolution_closure_init("resolution_closure_cs"),
  while length(nF) > length(F) do (
    F : nF, nF : add_resolvents_cs(F),
    history : append(history, [length(nF)]),
    monitorcheck_resolution_closure_loop("resolution_closure_cs")
  ),
  return([nF, rest(history,-1)]))$


/* Adds all resolvents to a clause-set and then applies
   subsumption-elimination: */
min_add_resolvents_cs(F) := min_elements(add_resolvents_cs(F))$

/* The prime-implicates of a clause-set F (as CNF), and the list of
   clause-counts for the successive extensions of F (until a fixed point is
   reached): */
min_resolution_closure_cs(F) := block(
 [nF : min_add_resolvents_cs(F), history : [length(F)]],
  history : append(history, [length(nF)]),
  monitorcheck_resolution_closure_init("min_resolution_closure_cs"),
  while nF # F do (
    F : nF, nF : min_add_resolvents_cs(F),
    history : append(history, [length(nF)]),
    monitorcheck_resolution_closure_loop("min_resolution_closure_cs")
  ),
  return([nF, rest(history,-1)]))$
/* A basic equality is
   min_resolution_closure_cs(F)[1] = min_elements(resolution_closure_cs(F)[1]).
   Another basic equality (yielding an alternative computation) is
   min_resolution_closure_cs(F)[1] = min_2resolution_closure_cs(expand_fcs(cs2fcs(F))[2]).
*/


/* *****************
   * DP resolution *
   *****************
*/

/* The resolvable 2-sets of a clause-set on a given resolution literal: */
resolvable_cs_l(F,x) :=
  subset(powerset(F,2), lambda([Cs], resolvable_s_l(Cs,x)))$

/* All resolvents of a clause-set on a given resolution literal: */
resolvents_cs_l(F,x) :=
  map(lambda([S], resolvent_s_l(S,x)), resolvable_cs_l(F,x))$

/* The DP-operator in one step for clause-set F and resolution literal v: */
/* RENAME: dp_operator_cs */
dp_operator(F,v) := block(
 [Fv : subset(F, lambda([C], not disjointp(C, {v,-v})))],
  union(setdifference(F, Fv), resolvents_cs_l(Fv,v)))$
dp_operator_cs(F,v) := dp_operator(F,v)$
dp_operator_fcs(FF,v) := [disjoin(v,FF[1]), dp_operator_cs(FF[2],v)]$

/* DP combined with subsumption elimination: */
/* RENAME: min_dp_operator_cs */
min_dp_operator(F,v) := min_elements(dp_operator(F,v))$
min_dp_operator_cs(F,v) := min_dp_operator(F,v)$

/* Returns a list of pairs [s,c], where s is the size of DP-procedure-run
   (the sum of sizes of the clause-sets) and c is the count, where the list
   is sorted by ascending s: */
distribution_min_dp(FF) := block(
 [P : permutations(FF[1]), l : length(FF[2]), hash, counts : {}],
  for p in P do block([F : FF[2], s : l],
    for v in p do (
      F : min_dp_operator(F,v),
      s : s + length(F)
    ),
    if elementp(s, counts) then hash[s] : hash[s] + 1 else (
      counts : adjoin(s, counts), hash[s] : 1)
  ),
  return(create_list([s, hash[s]], s, listify(counts))))$


/* ****************************
   * Width-bounded resolution *
   ****************************
*/

/* Checks whether two clauses are k-resolvable: */
kresolvable(C,D,k) := resolvable(C,D) and (length(C)<=k or length(D)<=k)$
/* Checks whether a set of clauses is k-resolvable: */
kresolvable_s(S,k) := resolvable_s(S) and
  (length(first(S))<=k or length(second(S))<=k)$
/* The k-resolvable 2-sets of a clause-set: */
kresolvable_cs(F,k) := block([LF : l2array(listify(F)), l : length(F), R : []],
 for i : 1 thru l-1 do block([C : LF[i]],
  for j : i+1 thru l do
   if kresolvable(C,LF[j],k) then R : cons({C,LF[j]}, R)),
 setify(R))$
/*
  kresolvable_cs(F,k) = subset(powerset(F,2), lambda([S],kresolvable_s(S,k)))
*/


/* All k-resolvents of a clause-set: */
kresolvents_cs(F,k) := map(resolvent_s, kresolvable_cs(F,k))$
/* Adds all k-resolvents to a clause-set: */
add_kresolvents_cs(F,k) := union(F, kresolvents_cs(F,k))$


monitorcheck_kresolution_closure_init(name) := if oklib_monitor then (
  print(sconcat("M[",name,"]:"), history))$
monitorcheck_kresolution_closure_loop(name) := if oklib_monitor then (
  print(last(history)))$

/* The k-resolution (asymmetric width) closure of a clause-set, returning a
   pair, where the second element is the list of clause-counts for the
   stages:
*/
kresolution_closure_cs(F,k) := block(
 [nF : add_kresolvents_cs(F,k), history : [length(F)]],
  history : append(history, [length(nF)]),
  monitorcheck_kresolution_closure_init("kresolution_closure_cs"),
  while length(nF) > length(F) do (
    F : nF, nF : add_kresolvents_cs(F,k),
    history : append(history, [length(nF)]),
    monitorcheck_kresolution_closure_loop("kresolution_closure_cs")
  ),
  return([nF, rest(history,-1)]))$

/* Adds all k-resolvents to a clause-set and then applies
   subsumption-elimination: */
min_add_kresolvents_cs(F,k) := min_elements(add_kresolvents_cs(F,k))$

/* The minimal clauses derivable by k-resolution of a clause-set F,
   and the list of clause-counts for the successive extensions of F (until a
   fixed point is  reached):
*/
min_kresolution_closure_cs(F,k) := block(
 [nF : min_add_kresolvents_cs(F,k), history : [length(F)]],
  history : append(history, [length(nF)]),
  monitorcheck_kresolution_closure_init("min_kresolution_closure_cs"),
  while nF # F do (
    F : nF, nF : min_add_kresolvents_cs(F,k),
    history : append(history, [length(nF)]),
    monitorcheck_kresolution_closure_loop("min_kresolution_closure_cs")
  ),
  return([nF, rest(history,-1)]))$
/* A basic equality is
    min_kresolution_closure_cs(F,k)[1] =
      min_elements(kresolution_closure_cs(F,k)[1]).
*/


/* Checks whether two clauses are symmetric k-resolvable
   ("bounded resolution"):
*/
bresolvable(C,D,k) := length(C)<=k and length(D)<=k and resolvable(C,D) and
 length(resolvent(C,D))<=k$
/* Checks whether a set of clauses is symmetric k-resolvable: */
bresolvable_s(S,k) := length(S)=2 and bresolvable(first(S),second(S),k)$
/* The symmetric k-resolvable 2-sets of a clause-set: */
bresolvable_cs(F,k) := block([LF : l2array(listify(F)), l : length(F), R : []],
 for i : 1 thru l-1 do block([C : LF[i]],
  for j : i+1 thru l do
   if bresolvable(C,LF[j],k) then R : cons({C,LF[j]}, R)),
 setify(R))$
/*
  bresolvable_cs(F,k) = subset(powerset(F,2), lambda([S],bresolvable_s(S,k)))
*/


/* All symmetric k-resolvents of a clause-set: */
bresolvents_cs(F,k) := map(resolvent_s, bresolvable_cs(F,k))$
/* Adds all symmetric k-resolvents to a clause-set: */
add_bresolvents_cs(F,k) := union(F, bresolvents_cs(F,k))$


/* The symmetric k-resolution closure of a clause-set, returning a pair, where
   the second element is the list of clause-counts for the stages:
*/
bresolution_closure_cs(F,k) := block(
 [nF : add_bresolvents_cs(F,k), history : [length(F)]],
  history : append(history, [length(nF)]),
  monitorcheck_kresolution_closure_init("bresolution_closure_cs"),
  while length(nF) > length(F) do (
    F : nF, nF : add_bresolvents_cs(F,k),
    history : append(history, [length(nF)]),
    monitorcheck_kresolution_closure_loop("bresolution_closure_cs")
  ),
  return([nF, rest(history,-1)]))$

/* Adds all symmetric k-resolvents to a clause-set and then applies
   subsumption-elimination: */
min_add_bresolvents_cs(F,k) := min_elements(add_bresolvents_cs(F,k))$

/* The minimal clauses derivable by symmetric k-resolution of a clause-set F,
   and the list of clause-counts for the successive extensions of F (until a
   fixed point is  reached):
*/
min_bresolution_closure_cs(F,k) := block(
 [nF : min_add_bresolvents_cs(F,k), history : [length(F)]],
  history : append(history, [length(nF)]),
  monitorcheck_kresolution_closure_init("min_bresolution_closure_cs"),
  while nF # F do (
    F : nF, nF : min_add_bresolvents_cs(F,k),
    history : append(history, [length(nF)]),
    monitorcheck_kresolution_closure_loop("min_bresolution_closure_cs")
  ),
  return([nF, rest(history,-1)]))$
/* A basic equality is
    min_bresolution_closure_cs(F,k)[1] =
      min_elements(bresolution_closure_cs(F,k)[1]).
*/


/* *******************
   * Blocked clauses *
   *******************
*/

/* A clause C is blocked for a clause-set F if there exists a literal l in C,
   called the blocking literal, such that no resolution in F on l with C
   is possible.

   A blocked extension of a clause-set F is a clause-set F' such that
   the (confluent) result of removing blocked clauses from union(F',F)
   and F yields the same result. This is equivalent to checking whether
   the result of removing blocked clauses from union(F',F) is a subset of F.
*/

/* Checking whether l is a blocking literal in a clause C for clause-set F: */
blocking_literal_p(F,C,l) :=
  elementp(l,C) and
    every_s(lambda([D], not(elementp(-l,D)) or not(resolvable(C,D))), F)$

/* Checking whether the clause C is blocked for F: */
blocked_cs_p(F,C) := some_s(lambda([l], blocking_literal_p(F,C,l)), C)$

/* Eliminating blocked clauses from F: */
elim_blocked_cs(F) := block([tF : F, had_blocked : true],
  while had_blocked do block([F_blocked],
    F_blocked : subset(tF, lambda([C], blocked_cs_p(tF,C))),
    if emptyp(F_blocked) then had_blocked : false
    else tF : setdifference(tF, F_blocked)),
  return(tF))$
/* Eliminating blocked clauses of length at most k from F: */
elim_blockedk_cs(F,k) := block([tF : F, had_blocked : true],
  while had_blocked do block([F_blocked],
    F_blocked : subset(tF, lambda([C], is(length(C)<=k) and blocked_cs_p(tF,C))),
    if emptyp(F_blocked) then had_blocked : false
    else tF : setdifference(tF, F_blocked)),
  return(tF))$


/* Checking whether eF is a blocked extension of F: */
blocked_extension_cs_p(eF,F) :=
  subsetp(elim_blocked_cs(union(eF,F)), F)$


/* ****************
   * DRAT clauses *
   ****************
*/

/* Test whether clause C is a DRAT-clause for clause-set F and literal l: */
drat_literal_p(F,C,l) :=
  elementp(l,C) and
    every_s(lambda([D],
      not(elementp(-l,D)) or
      not(resolvable(C,D)) or
      ucp_0_cs(add_neg_cl_cs(F,union(C,disjoin(-l,D)))) = {{}}),
    F)$
drat_literal_analyse(F,C,l) := for D in F do
  if elementp(-l,D) and resolvable(C,D) then
    print(D, ucp_0_cs(add_neg_cl_cs(F,union(C,disjoin(-l,D)))))$

/* Test whether clause C is a DRAT-clause for F: */
drat_cs_p(F,C) := some_s(lambda([l], drat_literal_p(F,C,l)), C)$

/* All additional DRAT-clause without new variables and of length k for F: */
all_add_drat_cs(F,k) := subset(setdifference(all_pass_n(var_cs(F),k), F), lambda([C], drat_cs_p(F,C)))$

/* Find some additional shortest DRAT-clause without new variables: */
some_shortadd_drat_cs(F) := block([V:var_cs(F), n:nvar_cs(F), res],
  for k : 1 thru n do (
    res : (
      for C in setdifference(all_pass_n(V,k), F) do
        if drat_cs_p(F,C) then return(C)
    ), if res # done then return(res)))$

/* Add DRAT-clauses until ucp realises unsatisfiability or satisfiability: */
rep_shortadd_drat_cs(F) := block([res:[], ucp],
  do (
    ucp : ucp_0_cs(F),
    if ucp = {{}} then return(reverse(cons(false,res))),
    if ucp = {} then return(reverse(cons(true,res))),
    res : cons(some_shortadd_drat_cs(F), res),
    F : adjoin(first(res), F)
 ))$

/*
TODO: prove that rep_shortadd_drat_cs(F) always terminates.
TODO: randomise rep_shortadd_drat_cs (with seed).
TODO: integrate into the DRAT-test the case, that a satisfying assignment
was found, and obtain in this way a more efficient decision procedure.
TODO: offer to use ucp as a reduction in rep_shortadd_drat_cs.
TODO: In general, addition of two drat-clauses from all_add_drat_cs(F,k)
(at the same time) is compatible (example needed), but one would guess
that doesn't hold for all_add_drat_cs(F,1) ?! So it should be possible
to always compute all_add_drat_cs(F,1), and to eliminate all these
unit-clauses (and the latter should not "reduce power"?!).
*/

/*!
\endhtmlonly
*/
