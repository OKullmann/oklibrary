/* Oliver Kullmann, 27.3.2008 (Swansea) */
/* Copyright 2008, 2010, 2011, 2012, 2013, 2016 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Satisfiability/Lisp/Resolution/tests/Basics.mac
  \brief Tests for basic resolution functionality

Use by

oklib_load("OKlib/ComputerAlgebra/Satisfiability/Lisp/Resolution/tests/Basics.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/TestSystem/Lisp/Asserts.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Generators/Generators.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/BasicOperations.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Generators/LinearEquations.mac")$
oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/SetSystems.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/PartialAssignments.mac")$

kill(f)$


/* ***************************
   * Single resolution steps *
   ***************************
*/

okltest_resolvable(f) := (
  assert(f({},{}) = false),
  assert(f({1},{}) = false),
  assert(f({},{1}) = false),
  assert(f({1},{-1}) = true),
  assert(f({1,-2,3},{1,2,4}) = true),
  assert(f({1,2},{2,3,4}) = false),
  true)$

okltest_resolution_literal(f) := (
  assert(f({1},{-1}) = 1),
  assert(f({1,2,-3},{2,3,-4}) = -3),
  true)$

okltest_resolvable_p(f) := (
  assert(f({},{}) = []),
  assert(f({1},{}) = []),
  assert(f({},{1}) = []),
  assert(f({1},{-1}) = [1]),
  assert(f({1,-2,3},{1,2,4}) = [-2]),
  assert(f({1,2},{2,3,4}) = []),
  true)$

okltest_two_subsumption_resolvent_p(f) := (
  assert(f({},{}) = []),
  assert(f({1},{-1}) = [{}]),
  assert(f({1},{-1,2}) = []),
  assert(f({1},{2}) = []),
  assert(f({1,2},{-1,2}) = [{2}]),
  assert(f({1,2},{-1,-2}) = []),
  true)$


/* *************************
   * Adding all resolvents *
   *************************
*/

okltest_resolvable_cs(f) := (
  assert(f({}) = {}),
  assert(f({{}}) = {}),
  assert(f({{1}}) = {}),
  assert(f({{1},{-1}}) = {{{1},{-1}}}),
  assert(f({{1},{-1},{}}) = {{{1},{-1}}}),
  assert(f({{1,2},{-1,2},{-1,-2}}) = {{{1,2},{-1,2}},{{-1,2},{-1,-2}}}),
  for n : 0 thru 4 do block([F : full_fcs(n)[2]],
    assert(length(f(F)) = 2^n * n / 2)),
  true)$

okltest_two_subsumption_resolvable_cs(f) := (
  assert(f({}) = {}),
  assert(f({{}}) = {}),
  assert(f({{1}}) = {}),
  assert(f({{1},{-1}}) = {{{1},{-1}}}),
  assert(f({{1},{-1},{}}) = {{{1},{-1}}}),
  assert(f({{1,2},{-1,2},{-1,-2}}) = {{{1,2},{-1,2}},{{-1,2},{-1,-2}}}),
  assert(f({{1,2},{-1,2},{-1,-2},{1}}) = {{{1,2},{-1,2}},{{-1,2},{-1,-2}}}),
  for n : 0 thru 4 do block([F : full_fcs(n)[2]],
    assert(length(f(F)) = 2^n * n / 2)),
  true)$

okltest_two_subsumption_resolvents_rem_cs(f) := (
  assert(f({}) = [{},{}]),
  assert(f({{}}) = [{},{{}}]),
  assert(f({{1}}) = [{},{{1}}]),
  assert(f({{1},{-1}}) = [{{}},{}]),
  assert(f({{1,2},{1,-2}}) = [{{1}},{}]),
  assert(f({{1,2},{-1,-2}}) = [{},{{1,2},{-1,-2}}]),
  true)$


/* *****************
   * DP resolution *
   *****************
*/

okltest_dp_operator(f) := (
  assert(f({},1) = {}),
  assert(f({{}},1) = {{}}),
  assert(f({{1}},1) = {}),
  assert(f({{1}},2) = {{1}}),
  assert(f({{1},{-1}},1) = {{}}),
  assert(f({{1},{-1}},2) = {{1},{-1}}),
  block([n : 4, F],
    F : full_fcs(n)[2],
    for v : n thru 1 step -1 do (
      F : f(F,v),
      assert(F = full_fcs(v-1)[2]))),
  true)$

okltest_dp_operator_fcs(f) := (
  assert(f([{},{}],1) = [{},{}]),
  assert(f([{},{{}}],1) = [{},{{}}]),
  assert(f([{1},{}],1) = [{},{}]),
  assert(f([{1},{{}}],1) = [{},{{}}]),
  assert(f([{2},{}],1) = [{2},{}]),
  assert(f([{2},{{}}],1) = [{2},{{}}]),
  assert(okltest_dp_operator(buildq([f], lambda([F,v], fcs2cs(f(cs2fcs(F),v))))) = true),
  true)$

okltest_distribution_min_dp(f) := block(
  assert(f([{},{}]) = [[0,1]]),
  assert(f([{},{{}}]) = [[1,1]]),
  assert(f([{1},{}]) = [[0,1]]),
  assert(f([{1},{{}}]) = [[2,1]]),
  assert(f([{1},{{1}}]) = [[1,1]]),
  assert(f([{1},{{1},{-1}}]) = [[3,1]]),
  assert(f([{1},{{},{1},{-1}}]) = [[4,1]]),
  assert(f([{1,2},{{1,2},{-1,2},{-1,-2}}]) = [[4,2]]),
  assert(f([{1,2},{{1},{1,2},{-1,2},{-1,-2}}]) = [[7,2]]),
  for n : 0 thru if oklib_test_level=0 then 3 else 4 do
   block([FF : full_fcs(n)],
    assert(f(FF) = [[2^(n+1)-1,n!]])),
  true)$


/* ****************************
   * Width-bounded resolution *
   ****************************
*/

okltest_kresolvable(f) := (
  assert(f({},{},0) = false),
  assert(f({1},{-1},0) = false),
  assert(f({1},{-1},1) = true),
  assert(f({1},{-1,2},1) = true),
  assert(f({1,2},{-1},1) = true),
  assert(f({1,2},{-1,2},1) = false),
  assert(f({1,2},{-1,2},2) = true),
  assert(f({1,2,3},{-1,2},2) = true),
  assert(f({1,2},{-1,2,3},2) = true),
  true)$

okltest_kresolvable_s(f) := (
  assert(okltest_kresolvable(buildq([f], lambda([C,D,k], f({C,D},k)))) = true),
  assert(f({},0) = false),
  assert(f({{1}},0) = false),
  assert(f({{1},{}},0) = false),
  assert(f({{1},{-1}},0) = false),
  assert(f({{1},{-1}},1) = true),
  assert(f({{1},{-1,2}},1) = true),
  assert(f({{1,2},{-1,2}},1) = false),
  assert(f({{1,2},{-1,2}},2) = true),
  assert(f({{1,2,3},{-1,2}},2) = true),
  true)$

okltest_kresolvable_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {}),
  assert(f({{1},{-1}},0) = {}),
  assert(f({{1},{-1}},1) = {{{1},{-1}}}),
  assert(f({{1},{-1},{-1,2}},1) = {{{1},{-1}},{{1},{-1,2}}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{{1},{-1}},{{1},{-1,2}},{{1,2},{-1}}}),
  true)$

okltest_kresolvents_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {}),
  assert(f({{1},{-1}},0) = {}),
  assert(f({{1},{-1}},1) = {{}}),
  assert(f({{1},{-1},{-1,2}},1) = {{},{2}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{},{2}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},1) = {{},{2,4},{2,3}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},2) = {{},{2,4},{2,3}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},3) = {{},{2,4},{2,3},{2,3,4}}),
  true)$

okltest_add_kresolvents_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {{1}}),
  assert(f({{1},{-1}},0) = {{1},{-1}}),
  assert(f({{1},{-1}},1) = {{},{1},{-1}}),
  assert(f({{1},{-1},{-1,2}},1) = {{},{2},{1},{-1},{-1,2}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{},{2},{1},{1,2},{-1},{-1,2}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},1) = {{},{2,4},{2,3},{1},{1,2,4},{-1},{-1,2,3}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},2) = {{},{2,4},{2,3},{1},{1,2,4},{-1},{-1,2,3}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},3) = {{},{2,4},{2,3},{2,3,4},{1},{1,2,4},{-1},{-1,2,3}}),
  true)$

okltest_kresolution_closure_cs(f) := (
  assert(f({},0) = [{},[0]]),
  assert(f({},1) = [{},[0]]),
  assert(f({{}},0) = [{{}},[1]]),
  assert(f({{}},1) = [{{}},[1]]),
  assert(f({{1}},0) = [{{1}},[1]]),
  assert(f({{1}},1) = [{{1}},[1]]),
  assert(f({{1},{-1}},0) = [{{1},{-1}},[2]]),
  assert(f({{1},{-1}},1) = [{{},{1},{-1}},[2,3]]),
  for n : 1 thru 5 do block([N:setn(n), NN:singletons(setmn(-n,-1))],
    assert(f(adjoin(N,NN),1)[1] = union(NN,powerset(N)))),
  assert(f({{1,2},{-1,2}},1) = [{{1,2},{-1,2}},[2]]),
  assert(f({{1,2},{-1,2}},2) = [{{1,2},{-1,2},{2}},[2,3]]),
  assert(f({{1,2},{-1,2},{1,-2},{-1,-2}},2) = [{{1,2},{-1,2},{1,-2},{-1,-2},{1},{-1},{2},{-2},{}},[4,8,9]]),
  assert(f({{1,2,3},{-1,2,3}},2) = [{{1,2,3},{-1,2,3}},[2]]),
  assert(f({{1,2,3},{-1,2,3}},3) = [{{1,2,3},{-1,2,3},{2,3}},[2,3]]),
  true)$

okltest_min_add_kresolvents_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {{1}}),
  assert(f({{1},{-1}},0) = {{1},{-1}}),
  assert(f({{1},{-1}},1) = {{}}),
  assert(f({{1},{-1},{-1,2}},1) = {{}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},1) = {{}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},2) = {{}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},3) = {{}}),
  assert(f({{1,2},{2,3}},2) = {{1,2},{2,3}}),
  assert(f({{1,2},{-2,3}},2) = {{1,2},{-2,3},{1,3}}),
  true)$

okltest_min_kresolution_closure_cs(f) := (
  assert(f({},0) = [{},[0]]),
  assert(f({},1) = [{},[0]]),
  assert(f({{}},0) = [{{}},[1]]),
  assert(f({{}},1) = [{{}},[1]]),
  assert(f({{1}},0) = [{{1}},[1]]),
  assert(f({{1}},1) = [{{1}},[1]]),
  assert(f({{1},{-1}},0) = [{{1},{-1}},[2]]),
  assert(f({{1},{-1}},1) = [{{}},[2,1]]),
  for n : 1 thru 5 do block([N:setn(n), NN:singletons(setmn(-n,-1))],
    assert(f(adjoin(N,NN),1)[1] = {{}})),
  assert(f({{1,2},{-1,2}},1) = [{{1,2},{-1,2}},[2]]),
  assert(f({{1,2},{-1,2}},2) = [{{2}},[2,1]]),
  assert(f({{1,2},{-1,2},{1,-2},{-1,-2}},2) = [{{}},[4,4,1]]),
  assert(f({{1,2,3},{-1,2,3}},2) = [{{1,2,3},{-1,2,3}},[2]]),
  assert(f({{1,2,3},{-1,2,3}},3) = [{{2,3}},[2,1]]),
  true)$

okltest_bresolvable(f) := (
  assert(f({},{},0) = false),
  assert(f({1},{-1},0) = false),
  assert(f({1},{-1},1) = true),
  assert(f({1},{-1,2},1) = false),
  assert(f({1},{-1,2},2) = true),
  assert(f({1,2},{-2},1) = false),
  assert(f({1,2},{-2},2) = true),
  true)$

okltest_bresolvable_s(f) := (
  assert(f({},0) = false),
  assert(f({{}},0) = false),
  assert(f({{},{1}},1) = false),
  assert(f({{1},{-1}},1) = true),
  assert(okltest_bresolvable(buildq([f], lambda([C,D,k], f({C,D},k)))) = true),
  true)$

okltest_bresolvable_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {}),
  assert(f({{1},{-1}},0) = {}),
  assert(f({{1},{-1}},1) = {{{1},{-1}}}),
  assert(f({{1},{-1},{-1,2}},1) = {{{1},{-1}}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{{1},{-1}}}),
  assert(f({{1,2},{-2,3},{-3}},1) = {}),
  assert(f({{1,2},{-2,3},{-3}},2) = {{{1,2},{-2,3}},{{-2,3},{-3}}}),
  assert(f({{1,2},{-2,3},{-3},{1,2,3}},2) = {{{1,2},{-2,3}},{{-2,3},{-3}}}),
  true)$

okltest_bresolvents_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {}),
  assert(f({{1},{-1}},0) = {}),
  assert(f({{1},{-1}},1) = {{}}),
  assert(f({{1},{-1},{-1,2}},1) = {{}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{}}),
  assert(f({{1,2},{-2,3},{-3}},1) = {}),
  assert(f({{1,2},{-2,3},{-3}},2) = {{1,3},{-2}}),
  assert(f({{1,2},{-2,3},{-3},{1,2,3}},2) = {{1,3},{-2}}),
  true)$

okltest_add_bresolvents_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {{1}}),
  assert(f({{1},{-1}},0) = {{1},{-1}}),
  assert(f({{1},{-1}},1) = {{},{1},{-1}}),
  assert(f({{1},{-1},{-1,2}},1) = {{},{1},{-1},{-1,2}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{},{1},{1,2},{-1},{-1,2}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},1) = {{},{1},{1,2,4},{-1},{-1,2,3}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},2) = {{},{1},{1,2,4},{-1},{-1,2,3}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},3) = {{},{2,4},{2,3},{2,3,4},{1},{1,2,4},{-1},{-1,2,3}}),
  true)$

okltest_bresolution_closure_cs(f) := (
  assert(f({},0) = [{},[0]]),
  assert(f({},1) = [{},[0]]),
  assert(f({{}},0) = [{{}},[1]]),
  assert(f({{}},1) = [{{}},[1]]),
  assert(f({{1}},0) = [{{1}},[1]]),
  assert(f({{1}},1) = [{{1}},[1]]),
  assert(f({{1},{-1}},0) = [{{1},{-1}},[2]]),
  assert(f({{1},{-1}},1) = [{{},{1},{-1}},[2,3]]),
  for n : 2 thru 5 do block([N:setn(n), NN:singletons(setmn(-n,-1)), F],
    F : adjoin(N,NN),
    assert(f(F,1)[1] = F),
    assert(f(F,n-1)[1] = F),
    assert(f(F,n)[1] = union(NN,powerset(N)))
  ),
  assert(f({{1,2},{-1,2}},1) = [{{1,2},{-1,2}},[2]]),
  assert(f({{1,2},{-1,2}},2) = [{{1,2},{-1,2},{2}},[2,3]]),
  assert(f({{1,2},{-1,2},{1,-2},{-1,-2}},2) = [{{1,2},{-1,2},{1,-2},{-1,-2},{1},{-1},{2},{-2},{}},[4,8,9]]),
  assert(f({{1,2,3},{-1,2,3}},2) = [{{1,2,3},{-1,2,3}},[2]]),
  assert(f({{1,2,3},{-1,2,3}},3) = [{{1,2,3},{-1,2,3},{2,3}},[2,3]]),
  for n : 1 thru cokltl(3,5) do block([F : full_cs(n)],
    assert(f(F,n-1)[1] = F),
    assert(f(F,n)[1] = all_pass(setn(n)))
  ),
  true)$

okltest_min_add_bresolvents_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{1}},0) = {{1}}),
  assert(f({{1},{-1}},0) = {{1},{-1}}),
  assert(f({{1},{-1}},1) = {{}}),
  assert(f({{1},{-1},{-1,2}},1) = {{}}),
  assert(f({{1},{1,2},{-1},{-1,2}},1) = {{}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},1) = {{}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},2) = {{}}),
  assert(f({{1},{1,2,4},{-1},{-1,2,3}},3) = {{}}),
  assert(f({{1,2},{2,3}},2) = {{1,2},{2,3}}),
  assert(f({{1,2},{-2,3}},2) = {{1,2},{-2,3},{1,3}}),
  assert(f({{1,2,3},{-1,2}},2) = {{1,2,3},{-1,2}}),
  assert(f({{1,2,3},{-1,2}},3) = {{2,3},{-1,2}}),
  true)$

okltest_min_bresolution_closure_cs(f) := (
  assert(f({},0) = [{},[0]]),
  assert(f({},1) = [{},[0]]),
  assert(f({{}},0) = [{{}},[1]]),
  assert(f({{}},1) = [{{}},[1]]),
  assert(f({{1}},0) = [{{1}},[1]]),
  assert(f({{1}},1) = [{{1}},[1]]),
  assert(f({{1},{-1}},0) = [{{1},{-1}},[2]]),
  assert(f({{1},{-1}},1) = [{{}},[2,1]]),
  for n : 2 thru 5 do block([N:setn(n), NN:singletons(setmn(-n,-1)), F],
    F : adjoin(N,NN),
    assert(f(F,1)[1] = F),
    assert(f(F,n-1)[1] = F),
    assert(f(F,n)[1] = {{}})
  ),
  assert(f({{1,2},{-1,2}},1) = [{{1,2},{-1,2}},[2]]),
  assert(f({{1,2},{-1,2}},2) = [{{2}},[2,1]]),
  assert(f({{1,2},{-1,2},{1,-2},{-1,-2}},2) = [{{}},[4,4,1]]),
  assert(f({{1,2,3},{-1,2,3}},2) = [{{1,2,3},{-1,2,3}},[2]]),
  assert(f({{1,2,3},{-1,2,3}},3) = [{{2,3}},[2,1]]),
  for n : 1 thru cokltl(3,5) do block([F : full_cs(n)],
    assert(f(F,n-1)[1] = F),
    assert(f(F,n)[1] = {{}})
  ),
  true)$


/* *******************
   * Blocked clauses *
   *******************
*/

okltest_blocking_literal_p(f) := block(
  assert(f({{1}},{1},1)),
  assert(not(f({},{},1))),
  assert(not(f({{}},{},1))),
  assert(not(f({{1},{-1}},{1},1))),
  assert(f({{1,2},{1,-2,-3},{-1,3}},{1,-2,-3},1)),
  assert(f({{1,2},{1,-2,-3},{-1,3}},{1,-2,-3},-3)),
  assert(not(f({{1,2},{1,-2,-3},{-1,3}},{1,-2,-3},3))),
  assert(f({{1,2}},{-1,-2},-1) = true),
  true)$

okltest_blocked_cs_p(f) := block(
  assert(f({{1}},{1})),
  assert(not(f({},{}))),
  assert(not(f({{}},{}))),
  assert(not(f({{1},{-1}},{1}))),
  assert(f({{1,2},{1,-2,-3},{-1,3}},{1,-2,-3})),
  assert(f({{1,2},{1,-2,-3},{-1,3}},{1,-2,-3})),
  for i : 1 thru 3 do block([F : full_cs(i)],
    for C in F do assert(not(f(F,C)))),
  for i : 1 thru 3 do block([F : setify(even_parity_cl(i))],
    for C in F do assert(f(F,C))),
  true)$

okltest_elim_blocked_cs(f) := block(
  assert(f({}) = {}),
  assert(f({{}}) = {{}}),
  assert(f({{1}}) = {}),
  assert(f({{1},{-1}}) = {{1},{-1}}),
  assert(f({{1,2},{1,-2,-3},{-1,3}}) = {}),
  for i : 1 thru 3 do block([F : full_cs(i)],
    assert(f(F) = F)),
  for i : 1 thru 3 do block([F : setify(even_parity_cl(i))],
    assert(f(F) = {})),
  true)$

okltest_elim_blockedk_cs(f) := (
  assert(okltest_elim_blocked_cs(buildq([f], lambda([F], f(F,inf)))) = true),
  assert(f({{1}},0) = {{1}}),
  assert(f({{1}},1) = {}),
  assert(f({{1,2},{1,-2,-3},{-1,3}},2) = {{1,-2,-3}}),
  true)$

okltest_blocked_extension_cs_p(f) := block(
  assert(f({},{})),
  assert(f({{1}},{{1}})),
  assert(f({{1}},{})),
  assert(not(f({{-1}},{{1}}))),
  for i : 1 thru 3 do block([F : full_cs(i)],
    assert(f(F,F))),
  for i : 1 thru 3 do block([F : setify(even_parity_cl(i))],
    assert(f(F,{})),
    assert(not(f(F,setify(odd_parity_cl(i)))))),
  true)$


/* ****************
   * DRAT clauses *
   ****************
*/

okltest_drat_literal_p(f) := (
  assert(f({{-1},{}},{1},1) = true),
  /* XXX */
  true)$

okltest_drat_cs_p(f) := (
  assert(f({{-1},{}},{1}) = true),
  /* XXX */
  true)$

okltest_all_add_drat_cs(f) := (
  assert(f({},0) = {}),
  assert(f({{},{1}},0) = {}),
  assert(f({{},{1}},1) = {{-1}}),
  assert(f({{},{1},{2}},1) = {{-1},{-2}}),
  assert(f({{},{1},{2}},2) = full_cs(2)),
  /* XXX */
  true)$

/*!
\endhtmlonly
*/
