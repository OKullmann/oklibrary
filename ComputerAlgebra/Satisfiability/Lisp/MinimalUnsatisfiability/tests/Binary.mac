/* Oliver Kullmann, 16.6.2018 (Swansea) */
/* Copyright 2018 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/tests/Binary.mac
  \brief Tests for functions related to 2-MU

Use by

oklib_load("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/tests/Binary.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/TestSystem/Lisp/Asserts.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/Binary.mac")$

kill(f)$

/* ****************************
   * All 2-MUs by brute force *
   ****************************
*/

okltest_all_ubmu(f) := (
  assert(f(0) = {}),
  assert(f(1) = {}),
  assert(f(2) = {{{1,2},{-1,2},{1,-2},{-1,-2}}}),
  true)$

okltest_all_ubmue(f) := (
  assert(f(0) = {}),
  assert(f(1) = {}),
  assert(f(2) = {{{1,2},{-1,2},{1,-2},{-1,-2}}}),
  true)$

okltest_all_bmu(f) := (
  assert(f(0) = {{{}}}),
  assert(f(1) = {{{}},{{1},{-1}}}),
  assert(length(f(2)) = 12),
  true)$

okltest_all_bmue(f) := (
  assert(f(0) = {{{}}}),
  assert(f(1) = {{{1},{-1}}}),
  assert(length(f(2)) = 9),
  true)$

/* ******************************
   * Some small auxiliary 2-CNF *
   ******************************
*/

okltest_impl_cl(f) := (
  assert(f(1,1) = []),
  assert(f(1,2) = [{-1,2}]),
  assert(f(1,3) = [{-1,2},{-2,3}]),
  assert(f(4,5) = [{-4,5}]),
  true)$

okltest_impl_cs(f) := (
  assert(f(2,1) = {}),
  assert(f(6,7) = {{-6,7}}),
  true)$


/* ***********************
   * The nonsingular B_n *
   ***********************
*/

okltest_nonsing_bmu_fcl(f) := (
  assert(f(0) = [[],[{}]]),
  assert(f(1) = [[1],[{-1},{1}]]),
  assert(f(2) = [[1,2],[{-1,2},{1,-2},{-1,-2},{1,2}]]),
  assert(f(3) = [[1,2,3],[{-1,2},{1,-2},{-2,3},{2,-3},{-1,-3},{1,3}]]),
  true)$

okltest_nonsing_bmu_cl(f) := (
  assert(f(0) = [{}]),
  assert(okltest_nonsing_bmu_fcl(buildq([f], lambda([n], cl2fcl(f(n))))) = true),
  true)$

okltest_nonsing_bmu_fcs(f) := (
  assert(f(0) = [{},{{}}]),
  assert(f(1) = [{1},{{-1},{1}}]),
  assert(f(2) = [{1,2},{{-1,2},{1,-2},{-1,-2},{1,2}}]),
  assert(f(3) = [{1,2,3},{{-1,2},{1,-2},{-2,3},{2,-3},{-1,-3},{1,3}}]),
  true)$

okltest_nonsing_bmu_cs(f) := (
  assert(f(0) = {{}}),
  assert(okltest_nonsing_bmu_fcs(buildq([f], lambda([n], cs2fcs(f(n))))) = true),
  true)$


/* **********************
   * Cycle descriptions *
   **********************
*/

okltest_valid_triple_bmu(f) := (
  assert(f(0) = false),
  assert(f([]) = false),
  assert(f([0,0,0]) = false),
  assert(f([1,0,1]) = true),
  true)$

okltest_valid_cycledesc_bmu(f) := (
  assert(f(0) = false),
  assert(f([[1,0,1]]) = false),
  assert(f([[1,0,1],[1,0,1]]) = true),
  true)$

okltest_deficiency_cycledesc(f) := block([x],
  assert(f([[x,x,x],[x,x,x]]) = 2),
  true)$

okltest_ncl_cycledesc(f) := block([a1,b1,c1,a2,b2,c2],
  assert(f([[a1,b1,c1],[a2,b2,c2]]) = a1+b1+c1+a2+b2+c2),
  true)$

okltest_nlitocc_cycledesc(f) := block([a1,b1,c1,a2,b2,c2],
  assert(f([[a1,b1,c1],[a2,b2,c2]]) = 2*(a1+b1+c1+a2+b2+c2)),
  true)$

okltest_cycledesc2bmu_fcl(f) := (
  for n : 2 thru 6 do
    assert(f(create_list([1,0,1],i,1,n)) = nonsing_bmu_fcl(n)),
  true)$

okltest_cycledesc2bmu_cs(f) := (
  for n : 2 thru 6 do
    assert(f(create_list([1,0,1],i,1,n)) = nonsing_bmu_cs(n)),
  true)$


/*!
\endhtmlonly
*/
