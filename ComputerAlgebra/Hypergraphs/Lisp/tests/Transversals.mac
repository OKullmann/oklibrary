/* Oliver Kullmann, 3.2.2008 (Swansea) */
/* Copyright 2008 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Hypergraphs/Lisp/tests/Transversals.mac
  \brief Tests for transversal-functions

Use by

oklib_load("OKlib/ComputerAlgebra/Hypergraphs/Lisp/tests/Transversals.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/TestSystem/Lisp/Asserts.mac")$
oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/Generators.mac")$
oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/tests/IndependentSets.mac");

kill(f)$

check_involutiv_min(f,G) := block([G2 : f(f(G))],
  is(G2[1] = G[1] and G2[2] = min_elements(G[2])))$

/* Testing whether f computes the transversal hypergraph */
okltest_transversal_hyp(f) := block([G],
  assert(f([{},{}]) = [{},{{}}]),
  assert(f([{},{{}}]) = [{},{}]),
  assert(f([{1,2},{{1,2}}]) = [{1,2},{{1},{2}}]),
  assert(f([{1,2,3,4},{{1,2},{1,3},{1,4}}]) = [{1,2,3,4},{{1},{2,3,4}}]),
  assert(check_involutiv_min(f,[{1,2,3,4},{{1,2},{1,2,4},{2,3},{1,3}}])),
  for k : 0 thru 3 do
    for n : 0 thru 6 do
      assert(check_involutiv_min(f,ArithmeticProgressions(k,n))),
  if oklib_test_level = 0 then return(true),
  block([oklib_test_level : min(oklib_test_level-1, 2)],
    okltest_independence_hyp(buildq([f],lambda([G],ecomp_hyp(f(G)))))
  ),
true)$

/*!
\endhtmlonly
*/
