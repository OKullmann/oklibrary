/* Oliver Kullmann, 23.10.2010 (Swansea) */
/* Copyright 2010, 2011, 2012 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Hypergraphs/Lisp/Generators/VanderWaerden.mac
  \brief Tools related to the generation of hypergraphs of arithmetic progressions

Use by

oklib_load("OKlib/ComputerAlgebra/Hypergraphs/Lisp/Generators/VanderWaerden.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/SetSystems.mac")$
oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/Basics.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/Combinatorics/Lisp/Enumeration/Order.mac")$


/* ***************************
   * Arithmetic progressions *
   ***************************
*/

/* The arithmetic progression of length k and slope d, starting with a: */
arpr(k,a,d) := create_list(a + i*d, i,0,k-1)$
/* If d = 0, then the progression is degenerated. */

/* Testing whether the list L represents an arithmetic progression: */
arithmetic_progression_p(L) := if length(L) <= 1 then true
  else arithmetic_progression_d_p(rest(L),L[2] - L[1])$
/* Testing whether the list L represents an arithmetic progression with
   (constant) difference d: */
arithmetic_progression_d_p(L,d) := if length(L) <= 1 then true
  else is(second(L) - first(L) = d) and arithmetic_progression_d_p(rest(L),d)$


/* ***************************************************
   * Standard hypergraphs of arithmetic progressions *
   ***************************************************
*/

/* The list of arithmetic progressions of length k, ending in vertex n
   for n, k >= 1, smallest start-vertex first:
*/
arithprog_finish(k,n) :=
 if k=1 then [{n}] else
  reverse(create_list(setify(create_list(n-d*i,i,0,k-1)), d,1,floor((n-1)/(k-1))))$


/* The hypergraph of all arithmetic progressions of size k in {1,...,n}.
   The order of hyperedges: Sorted by increasing last vertex, and inside such
   a segment by decreasing slope. This amounts to colexicographical
   ordering.
   Prerequisites: n,k are natural numbers >= 0.
*/
arithprog_ohg(k,n) := if k=0 then [create_list(i,i,1,n),[{}]]
 elseif n=0 then [[],[]] else
 [create_list(i,i,1,n),
  lappend(create_list(arithprog_finish(k,i),i,1,n))]$
arithprog_hg(k,n) := ohg2hg(arithprog_ohg(k,n))$

/* Statistics functions: */

nver_arithprog_hg(k,n) := n$
nver_arithprog_ohg(k,n) := n$

nhyp_arithprog_hg(k,n) := 
 if k=0 then 1
 elseif k=1 then n
 elseif n < k then 0
 else block([q : floor((n-1)/(k-1))], q * (n - 1/2*(k-1)*(q+1)))$
nhyp_arithprog_ohg(k,n) := nhyp_arithprog_hg(k,n)$


/* Statefree iteration through all arithmetic progressions of length k
   in the natural numbers, in colexicographical order.
*/
/* Prerequisite: k natural number >= 0: */
first_colex_arithprog(k) := create_list(i,i,1,k)$
/* Prerequisite: P ascending arithmetic progression of natural numbers >= 1: */
next_colex_arithprog(P) := block([k : length(P)],
 if k=0 then done
 elseif k=1 then [first(P)+1]
 else block([d : second(P) - first(P), l : last(P)],
  if d>1 then d : d-1
  else (d : floor(l/(k-1)), l : l+1),
  reverse(create_list(l-i*d,i,0,k-1))
 ))$


/* ******************************************************
   * Generalised hypergraphs of arithmetic progressions *
   ******************************************************
*/

/* The list of all arithmetic progressions of length k for the sorted list L
   (following the order of L, first all progressions starting with the
   first vertex, then with the second, ...). This amounts to lexicographical
   ordering.
   Prerequisites: k natural number >= 0, L ascending w.r.t. ">".
*/
arithmetic_progressions(L,k) :=
 if k = 0 then [[]]
 elseif k = 1 then map("[",L)
 else block([l : length(L)],
   if l <= 1 then return([]),
   block([x : first(L), res_without, res_with : []],
     L : rest(L), l : l - 1,
     res_without : arithmetic_progressions(L,k),
     /* Computing all arithmetic progressions starting with x: */
     while l > 0 do block([y : first(L), d, P, lp],
       L : rest(L), l : l - 1,
       d : y - x,
       P : [x,y], lp : 2,
       block([prev_el : y],
         for z in L unless lp = k or z - prev_el > d do
           if z - prev_el = d then (
             P : endcons(z,P), lp : lp + 1, prev_el : z
           )
       ),
       if lp = k then res_with : cons(P,res_with)
     ),
     return(append(reverse(res_with),res_without))))$

/* The hypergraph of all arithmetic progressions of size k in the
   list L of different integers: */
arithprog_list_ohg(k,L) := [L,map(setify,arithmetic_progressions(L,k))]$
arithprog_list_hg(k,L) := ohg2hg(arithprog_list_ohg(k,L))$

/* Testing whether the set S contains an arithmetic progressions of size k.
   Works if S is a set of integers. In general we need +,- and order, while
   the precise general assumptions have to be investigated.
*/
has_arithprog(S,k) :=
 if k<0 then false
 elseif k=0 then true
 elseif length(S) < k then false
 elseif k<=2 then true
 else block([x : first_element(S)],
   S : disjoin(x,S),
   some_s(lambda([y], subsetp(setify(rest(arpr(k,x,y-x),2)), S)), S)
   or has_arithprog(S,k))$


/* ************
   * Symmetry *
   ************
*/

/* VdW-hypergraphs G have the mirror symmetry m, defined as reflection on the
   middle point of the vertex-set. Now for an ordered vdW-hypergraph G we
   enforce that all colourings f are symmetric w.r.t. m, that is,
     f(v) = f(m(v)).
   More precisely:
   Let n be the number of vertices in G (so the vertices are 1, ..., n).
   Then m_n(v) = n-v+1. m is a bijection from {1,...,floor(n/2)} to 
   {ceil(n/2)+1,...,n}, while for odd n the vertex floor(n/2)+1=ceil(n/2)
   is fixed by m.
   Now the function palindromise_vdw_(o)hg(G) replaces every vertex v >= n/2+1
   by its mirror image m(v). The new vertices are 1, ..., ceil(n/2).
   Furthermore all subsumed clauses are removed from the obtained list of
   hyperedges, sorting the clauses then in colexicographical order.

   This amounts to considering for colourings of G only the palindromic
   colourings (those colourings which read forward and backward the same).
*/

/* First the function which creates that choice function which w.r.t. the
   equivalence relation created by a~b iff m_n(a) = b chooses as representative
   of {a,b} the smaller element. mirrorfold_vdw(n) is a function from {1,...,n}
   to {1,...,ceiling(n/2)} for natural numbers n >= 0.
*/
mirrorfold_vdw(n) := buildq([n,div:ceiling(n/2)],
  lambda([v], if v >= div+1 then n-v+1 else v))$

/* Now the palindromisation of vdW-hypergraphs (colexicographical order, with
   subsumption-elimination): */
palindromise_vdw_ohg(G) := 
 if emptyp(first(G)) then G
 else block([n : last(first(G)), div, V],
  div : ceiling(n/2),
  V : create_list(i,i,1,div),
  if emptyp(second(G)) or emptyp(first(second(G))) then return([V,second(G)])
  else return(
  [V,
   map(setify,
       sort(map(listify,
              min_elements_l(unique(
                map(lambda([H], map(mirrorfold_vdw(n), H)),
                    sublist(second(G), lambda([H], is(first(H) < div+1))))))),
            colex_lessp_l))]))$
palindromise_vdw_hg(G) :=
 if emptyp(first(G)) then G
 else block([n : last(first(G)), div, V],
  div : ceiling(n/2),
  V : setn(div),
  if emptyp(second(G)) or emptyp(first(second(G))) then return([V,second(G)])
  else return(
  [V,
   min_elements(
    map(
      lambda([H], map(mirrorfold_vdw(n), H)),
      subset(second(G), lambda([H], is(first(H) < div+1)))))]))$
/* Comment: both functions use that first(X), last(X) is the smallest/largest
   element of set X. */

/* Now the direct computation of palindromise_vdw_ohg(arithprog_ohg(k,n))
   and palindromise_vdw_hg(arithprog_hg(k,n)):
*/
arithprog_pd_hg(k,n) := block([div:ceiling(n/2),V,p1,G,mf,A],
  V : setn(div),
  if n<k then return([V,{}])
  elseif n=k then return([V,{V}])
  elseif k=0 then return([V,{{}}])
  elseif k=1 then return([V,singletons(V)])
  elseif k=2 then return([V,singletons(setn(floor(n/2)))]),
  p1 : lambda([x],x+1),
  G : map(lambda([H],map(p1,H)),listify(second(arithprog_pd_hg(k,n-2)))),
  mf : mirrorfold_vdw(n),
  A : sublist(map(lambda([H],map(mf,H)),arithprog_finish(k,n)), 
             lambda([H], every_s(lambda([E], not subsetp(E,H)), G))),
  A : min_elements_l(A),
  return([V, setify(append(G,A))]))$

/*!
\endhtmlonly
*/
