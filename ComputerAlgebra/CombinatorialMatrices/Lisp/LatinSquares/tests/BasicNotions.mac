/* Oliver Kullmann, 30.12.2008 (Swansea) */
/* Copyright 2008, 2009, 2011 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/CombinatorialMatrices/Lisp/LatinSquares/tests/BasicNotions.mac
  \brief Tests regarding basic notions for latin squares

Use by

oklib_load("OKlib/ComputerAlgebra/CombinatorialMatrices/Lisp/LatinSquares/tests/BasicNotions.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/TestSystem/Lisp/Asserts.mac")$
oklib_include("OKlib/ComputerAlgebra/CombinatorialMatrices/Lisp/Basics.mac")$
oklib_include("OKlib/ComputerAlgebra/Algebra/Lisp/Groupoids/Groups/CyclicGroups.mac")$
oklib_include("OKlib/ComputerAlgebra/Algebra/Lisp/Groupoids/BasicNotions.mac")$
oklib_include("OKlib/ComputerAlgebra/Algebra/Lisp/Groupoids/Quasigroups/Basics.mac")$
oklib_include("OKlib/ComputerAlgebra/Algebra/Lisp/Groupoids/Associativity.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/CombinatorialMatrices/Lisp/LatinSquares/BasicNotions.mac")$


kill(f)$

/* *****************************
   * Notions of "latin squares *
   *****************************
*/

okltest_comlso_p(f) := (
  /* XXX */
  true)$

/* Remark: The semantics of okltest_comls_p is identical with that of
   okltest_qgrp_p. Currently all tests are handled by okltest_comls_p.
*/
okltest_comls_p(f) := (
  assert(f(0) = false),
  assert(f([]) = false),
  assert(f([[],[]]) = false),
  assert(f([{},0]) = true),
  assert(f([{1},0]) = false),
  assert(f(scom_l([1])) = true),
  assert(f(scom_l([2])) = false),
  assert(f(scom_l([1,2],[2,1])) = true),
  assert(f(scom_l([2,1],[1,2])) = true),
  assert(f(scom_l([1,2],[2,2])) = false),
  for n : 1 thru 6 do
    assert(f(qgrp2comls(ugrp2grp(cyclic_ugrp(n)))) = true),
  true)$

okltest_ls_p(f) := (
  /* XXX */
  true)$

okltest_hrls_p(f) := (
  /* XXX */
  assert(f(matrix([2,1],[1,2])) = false),
  assert(f(matrix([1,2],[2,1])) = true),
  assert(f(matrix([1,3,2],[2,1,3],[3,2,1])) = true),
  true)$

okltest_rls_p(f) := (
  /* XXX */
  assert(f(matrix([2,1],[1,2])) = false),
  assert(f(matrix([1,2],[2,1])) = true),
  assert(f(matrix([1,2,3],[3,1,2],[2,3,1])) = false),
  assert(f(matrix([1,2,3],[2,3,1],[3,1,2])) = true),
  assert(f(matrix([1,3,2],[2,1,3],[3,2,1])) = false),
  for n : 1 thru 6 do
    assert(f(uqgrp2rls(cyclic_ugrp(n))) = true),
  true)$


/* *********************
   * Various diagonals *
   *********************
*/

okltest_main_diagonal(f) := (
  assert(f(0) = []),
  assert(f(1) = [[1,1]]),
  assert(f(2) = [[1,1],[2,2]]),
  true)$

okltest_side_diagonal(f) := (
  assert(f(0) = []),
  assert(f(1) = [[1,1]]),
  assert(f(2) = [[1,2],[2,1]]),
  assert(f(3) = [[1,3],[2,2],[3,1]]),
  true)$

okltest_main_pandiagonal(f) := (
  for n : 1 thru 6 do
    assert(f(n,0) = main_diagonal(n)),
  assert(f(2,1) = [[2,1],[1,2]]),
  assert(f(3,1) = [[2,1],[3,2],[1,3]]),
  assert(f(3,2) = [[3,1],[1,2],[2,3]]),
  assert(f(4,1) = [[2,1],[3,2],[4,3],[1,4]]),
  assert(f(4,2) = [[3,1],[4,2],[1,3],[2,4]]),
  assert(f(4,3) = [[4,1],[1,2],[2,3],[3,4]]),
  true)$

okltest_side_pandiagonal(f) := (
  for n : 1 thru 6 do
    assert(f(n,n-1) = side_diagonal(n)),
  assert(f(2,0) = [[1,1],[2,2]]),
  assert(f(3,1) = [[1,2],[2,1],[3,3]]),
  assert(f(3,0) = [[1,1],[2,3],[3,2]]),
  assert(f(4,2) = [[1,3],[2,2],[3,1],[4,4]]),
  assert(f(4,1) = [[1,2],[2,1],[3,4],[4,3]]),
  assert(f(4,0) = [[1,1],[2,4],[3,3],[4,2]]),
  true)$

okltest_dls_p(f) := block([a,b,c,d],
  assert(f(matrix()) = true),
  assert(f(matrix([1])) = true),
  assert(f(matrix([1,2],[2,1])) = false),
  assert(f(matrix([1,3,2],[a,2,b],[c,d,3])) = false),
  assert(f(spdl_abn_ls(5,2,1)) = true),
  assert(f(spdl_abn_ls(7,2,1)) = true),
  assert(f(cyc3_rls) = false),
  assert(f(cyc3_o_hrls) = false),
  assert(f(nassoc_rls) = false),
  assert(f(dk10_rls) = false),
  assert(f(dk10_o_hrls) = false),
  assert(f(psssodls_8_ls) = true),
  true)$

okltest_spls_p(f) := block([a,b,c,d],
  assert(f(matrix()) = true),
  assert(f(matrix([1])) = true),
  assert(f(matrix([1,2],[2,1])) = false),
  assert(f(matrix([1,3,2],[a,2,b],[c,d,3])) = false),
  assert(f(spdl_abn_ls(5,2,1)) = true),
  assert(f(spdl_abn_ls(7,2,1)) = true),
  assert(f(cyc3_rls) = false),
  assert(f(cyc3_o_hrls) = false),
  assert(f(nassoc_rls) = false),
  assert(f(dk10_rls) = false),
  assert(f(dk10_o_hrls) = false),
  assert(f(psssodls_8_ls) = false),
  true)$

okltest_pls_p(f) := block([a,b,c,d],
  assert(f(matrix()) = true),
  assert(f(matrix([1])) = true),
  assert(f(matrix([1,2],[2,1])) = false),
  assert(f(matrix([1,3,2],[a,2,b],[c,d,3])) = false),
  assert(f(spdl_abn_ls(5,2,1)) = true),
  assert(f(spdl_abn_ls(7,2,1)) = true),
  assert(f(cyc3_rls) = false),
  assert(f(cyc3_o_hrls) = false),
  assert(f(nassoc_rls) = false),
  assert(f(dk10_rls) = false),
  assert(f(dk10_o_hrls) = false),
  assert(f(psssodls_8_ls) = true),
  true)$


/* *****************
   * Orthogonality *
   *****************
*/

okltest_ocomls_p(f) := (
  assert(f(scom_l([1]),scom_l([1])) = true),
  assert(f(scom_l([1,2],[2,1]), scom_l([1,2],[2,1])) = false),
  assert(f(scom_l([1,2],[2,1]), scom_l([2,1],[1,2])) = false),
  assert(okltest_ols_p(buildq([f], lambda([A,B], f(m2scom(A),m2scom(B))))) = true),
  true)$

okltest_ols_p(f) := (
  /* XXX */
  assert(f(cyc3_rls, cyc3_o_hrls) = true),
  assert(f(dk10_rls,dk10_o_hrls) = true),
  true)$

okltest_mocomls_p(f) := (
  assert(okltest_ocomls_p(buildq([f],lambda([A,B], f([A,B])))) = true),
  assert(okltest_mols_p(buildq([f], lambda([L], f(map(m2scom,L))))) = true),
  /* XXX */
  true)$

okltest_mols_p(f) := (
  assert(okltest_ols_p((buildq([f],lambda([A,B], f([A,B]))))) = true),
  /* XXX */
  true)$

okltest_socomls_p(f) := (
  assert(okltest_sols_p(buildq([f], lambda([A], f(m2scom(A))))) = true),
  /* XXX */
  true)$

okltest_sols_p(f) := (
  assert(f(psssodls_8_ls) = true),
  /* XXX */
  true)$


/* ************
   * Symmetry *
   ************
*/

okltest_ssls_p(f) := (
  assert(f(matrix()) = true),
  assert(f(matrix([1])) = true),
  assert(f(matrix([1,2],[2,1])) = false),
  /* XXX */
  assert(f(psssodls_8_ls) = true),
  true)$


/* ***************************
   * Sudoku-like constraints *
   ***************************
*/

okltest_sdkbox_p(f) := block([x],
  assert(f(matrix()) = true),
  assert(f(matrix([x])) = true),
  assert(f(matrix([1,2],[2,1])) = false),
  assert(f(matrix([1,2,1,2],[3,4,3,4],[1,2,1,2],[3,4,3,4])) = true),
  /* XXX */
  true)$


/* ****************
   * Combinations *
   ****************
*/

okltest_sdk_p(f) := (
  assert(f(0) = false),
  assert(f(matrix()) = true),
  assert(f(matrix([])) = false),
  assert(f(matrix([0])) = false),
  assert(f(matrix([1])) = true),
  assert(f(matrix([1,2],[2,1])) = false),
  /* XXX */
  true)$

okltest_sodls_p(f) := block([x],
  assert(f(0) = false),
  assert(f(matrix()) = true),
  assert(f(matrix([x])) = false),
  assert(f(matrix([1])) = true),
  assert(f(psssodls_8_ls) = true),
  assert(f(psssodls_9_ls) = true),
  /* XXX */
  true)$

okltest_sssodls_p(f) := (
  assert(f(sssodls_4_ls) = true),
  assert(f(psssodls_8_ls) = true),
  assert(f(psssodls_9_ls) = true),
  /* XXX */
  true)$

okltest_psssodls_p(f) := (
  assert(f(psssodls_8_ls) = true),
  assert(f(psssodls_9_ls) = true),
  assert(f(psssodls_20_ls) = true),
  /* XXX */
  true)$


/* ************
   * Examples *
   ************
*/

okltest_cyc3_rls(f) := (
  assert(matrix_size(f) = [3,3]),
  assert(rls_p(f) = true),
  true)$

okltest_cyc3_o_hrls(f) := block([Q],
  assert(matrix_size(f) = [3,3]),
  assert(rls_p(f) = false),
  assert(hrls_p(f) = true),
  assert(ocomls_p(m2scom(f),m2scom(cyc3_rls)) = true),
  Q : ls2qgrp(f),
  assert(qgrp_p(Q) = true),
  assert(has_neutral_el_grd(Q) = false),
  assert(associative_bydef_grd(Q) = false),
  true)$

okltest_nassoc_rls(f) := block([Q],
  assert(matrix_size(f) = [5,5]),
  assert(rls_p(f) = true),
  Q : rls2uqgrp(f),
  assert(uqgrp_p(Q) = true),
  assert(associative_bydef_grd(ugrd2grd(Q)) = false),
  true)$

okltest_dk10_rls(f) := (
  assert(matrix_size(f) = [10,10]),
  assert(rls_p(f) = true),
  true)$

okltest_dk10_o_hrls(f) := (
  assert(matrix_size(f) = [10,10]),
  assert(rls_p(f) = false),
  assert(hrls_p(f) = true),
  assert(ocomls_p(m2scom(f),m2scom(dk10_rls)) = true),
  true)$

okltest_sssodls_4_ls(f) := (
  assert(matrix_size(f) = [4,4]),
  assert(sssodls_p(f) = true),
  assert(pls_p(f) = false),
  assert(sdk_p(f) = true),
  true)$

okltest_psssodls_8_ls(f) := (
  assert(matrix_size(f) = [8,8]),
  assert(psssodls_p(f) = true),
  assert(spls_p(f) = false),
  true)$

okltest_psssodls_9_ls(f) := (
  assert(matrix_size(f) = [9,9]),
  assert(psssodls_p(f) = true),
  assert(spls_p(f) = false),
  assert(sdk_p(f) = true),
  true)$

okltest_psssodls_20_ls(f) := (
  assert(matrix_size(f) = [20,20]),
  assert(psssodls_p(f) = true),
  assert(spls_p(f) = false),
  assert(sdk_p(f) = false),
  true)$


/* **************
   * Generators *
   **************
*/

okltest_spdl_abn_ls(f) := (
  for n : 3 thru if oklib_test_level=0 then 20 else 40 do
    if divide(n,2)[2]#0 and divide(n,3)[2]#0 then block([L:f(n,2,1)],
      assert(ls_p(L)),
      assert(spls_p(L))
  ),
  true)$

okltest_spdl_abn_ls_p(f) := (
  for n : 3 thru if oklib_test_level=0 then 20 else 40 do
    if divide(n,2)[2]#0 and divide(n,3)[2]#0 then
      assert(f(n,2,1) = true),
  true)$

/* ***************
   * Conversions *
   ***************
*/

okltest_qgrp2comls(f) := (
  /* XXX */
  true)$

okltest_uqgrp2rls(f) := (
  assert(f([{77},lambda([x,y],77),77]) = matrix([1])),
  assert(f(rls2uqgrp(nassoc_rls)) = nassoc_rls),
  for n : 1 thru 5 do block(
   [M : f(cyclic_ugrp(n)),
    L : create_list(i,i,1,n)],
    assert(M = apply(matrix, create_list(rotate(L,-i),i,0,n-1)))
  ),
  /* XXX */
  true)$


/*!
\endhtmlonly
*/
