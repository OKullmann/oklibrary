/* Oliver Kullmann, 27.7.2008 (Swansea) */
/* Copyright 2008, 2009, 2010, 2012 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/RamseyTheory/Lisp/Ramsey/Numbers.mac
  \brief Providing access to all known information on Ramsey numbers

Use by

oklib_load("OKlib/ComputerAlgebra/RamseyTheory/Lisp/Ramsey/Numbers.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$


/* ******************
   * Ramsey numbers *
   ******************
*/

/* A "Ramsey parameter tuple" is a pair [r,L], where r is the length of the
   hyperedges considered, the length of L is the number of colours/parts, and 
   each entry of L specifies the size of the complete r-hypergraph, whose 
   existence as (subgraph) the Ramsey numbers shall guarantee.
*/

/* Bounds for Ramsey numbers are given as integers if the exact bound is known.
 * and pairs [lower bounds, upper bounds] otherwise (which must be correct).
*/

/* Returns the current bounds known for the given Ramsey parameter tuple.
   In the case nothing is known, unknown is returned. */
ramsey(param_tuple) := 
  if ramseyr0_a(param_tuple)#[] then
    apply(ramseyr0,ramseyr0_a(param_tuple))
  elseif ramseytrivle_a(param_tuple)#[] then 
    apply(ramseytrivle,ramseytrivle_a(param_tuple))
  elseif ramseytriveq_a(param_tuple)#[] then 
    apply(ramseytriveq,ramseytriveq_a(param_tuple))
  elseif ramseytrivk_a(param_tuple)#[] then 
    apply(ramseytrivk,ramseytrivk_a(param_tuple))
  elseif ramseyg2_3_a(param_tuple)#[] then 
    apply(ramseyg2_3,ramseyg2_3_a(param_tuple))
  elseif ramseyg2_4_a(param_tuple)#[] then 
    apply(ramseyg2_4,ramseyg2_4_a(param_tuple))
  elseif ramseyg2_5_a(param_tuple)#[] then 
    apply(ramseyg2_5,ramseyg2_5_a(param_tuple))
  elseif ramseyg2_6_a(param_tuple)#[] then 
    apply(ramseyg2_6,ramseyg2_6_a(param_tuple))
  elseif ramseyg2_7_a(param_tuple)#[] then 
    apply(ramseyg2_7,ramseyg2_7_a(param_tuple))
  elseif ramseyg2_8_a(param_tuple)#[] then 
    apply(ramseyg2_8,ramseyg2_8_a(param_tuple))
  elseif ramseyg2_9_a(param_tuple)#[] then 
    apply(ramseyg2_9,ramseyg2_9_a(param_tuple))
  elseif ramseyg2_10_a(param_tuple)#[] then 
    apply(ramseyg2_10,ramseyg2_10_a(param_tuple))
  elseif ramseyg3_3_3_a(param_tuple)#[] then 
    apply(ramseyg3_3_3,ramseyg3_3_3_a(param_tuple))
  elseif ramseyhg3_2_4_a(param_tuple)#[] then 
    apply(ramseyhg3_2_4,ramseyhg3_2_4_a(param_tuple))
  else unknown$

/* Checking whether L is a valid Ramsey parameter tuple: */
ramsey_p(L) := 
  listp(L) and length(L) > 1 and
  integerp(L[1]) and L[1] >= 0 and 
  listp(L[2]) and length(L[2]) > 0 and every_s(integerp,L[2]) and
  every_s(lambda([a], is(a >= 0)), L[2])$

/*
  There are numerous different "cases" for the known Ramsey number and so the
  lookup or computation of these number is split into many functions, which are
  then combined in "ramsey".

  For each case, there are two functions "ramseyXXX_a" and "ramseyXXX" where 
  XXX is a short abbreviation for the name/concept associated with the case.

  The "ramseyXXX_a" function takes an arbitrary Ramsey parameter tuple and 
  returns either [], in which case this "case" does not apply to the given 
  tuple, or it returns a list of arguments for ramseyXXX, in which case it does
  and ramseyXXX, given these arguments will return the Ramsey number for that
  tuple.

  These "cases" are split into 4 main groups.

  The "trivial cases" are those which can be computed directly, based on the
  definition of the Ramsey numbers.

  The "two-colour graph cases" are those which are associated with Ramsey
  problems with tuples of the form [2,[p,q]], and their associated functions
  are "ramseyg2_p_a" and "ramseyg2_p", where "ramseyg2_p" takes q as an
  argument.

  The "multi-colour graph cases" are those which are associated with Ramsey
  problems with tuples of the form [2,[p1,...,pn], and their associated 
  functions are "ramseyg2_p1_..._p(n-1)_a" and "ramseyg2_p1_..._p(n-1)", where 
  "ramseyg2_p1_..._p(n-1)" takes "pn" as an argument.
  
  The "hypergraph cases" are those which are associated with Ramsey
  problems with tuples of the form [r,[p1,...,pn], and their associated 
  functions are "ramseygr_p1_..._p(n-1)_a" and "ramseygr_p1_..._p(n-1)", where 
  "ramseygr_p1_..._p(n-1)" takes "pn" as an argument.

*/

/* *****************
   * Trivial cases *
   *****************
*/

/* If L[1] = 0 then the set of all 0-subsets of any set is {{}}, 
   and so as soon as */

/* Given a Ramsey parameter tuple L, returns [0] if L[1] = 0 and 
   length(L[2]) > 0, otherwise it returns []. */
ramseyr0_a(L) :=
  if length(L) = 2 and listp(L[2]) and length(L[2]) > 0 and 
     L[1] = 0 then [last(L[2])]
  else []$
ramseyr0(z) := z$

/* If min(L[2]) < L[1] then the set of all L[1]-subsets of {1,..,min(L)} is 
   empty, and the Ramsey number is simply min(L).  */

/* Given a Ramsey parameter tuple L, returns a list with the minimum 
   element of L[2] if min(L[2]) < L[1], otherwise it returns []. */
ramseytrivle_a(L) :=
  if length(L) = 2 and listp(L[2]) and length(L[2]) > 0 and 
     integerp(L[1]) and integerp(first(L[2])) and
     L[1] >= 0 and L[2][1] >= 0 and
     first(L[2]) < L[1] then [first(L[2])]
  else []$
/* Given the minimum element "q" of a Ramsey parameter tuple's L[2], and
   assuming "q < L[2]", return the Ramsey number for this tuple. */
ramseytrivle(min_qi) := min_qi$


/* If min(L[2]) = L[1] then that part can not be used without making an 
   L[1]-clique, so there is simply the remaining Ramsey problem without that 
   part. */

/* Given a Ramsey parameter tuple L, returns the original L with the minimum
   element of L[2] removed if min(L[2]) = L[1], otherwise it returns []. */
ramseytriveq_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) > 0 and 
     integerp(L[1]) and integerp(first(L[2])) and
     L[1] >= 0 and L[2][1] >= 0 and
     first(L[2]) = L[1] then [[L[1],rest(L[2])]]
  else []$
/* Given a Ramsey parameter tuple L, returns the Ramsey number for this
   tuple. 
   
   The assumption is that this L has had a value removed from L[2] based
   on ramseytriveq_a. */
ramseytriveq(L) := ramsey(L)$


/* If length(L[2]) < 2 then the Ramsey number is simply the first time there 
   are enough vertices for a L[2][1]-clique, as there is only one part. */

/* Given a Ramsey parameter tuple L, returns a list with the first element
   of L[2] if length(L[2]) = 1, otherwise it returns []. */
ramseytrivk_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 1 and 
     integerp(L[1]) and integerp(first(L[2])) and
     L[1] >= 0 and L[2][1] >= 0 then [first(L[2])]
  else []$
/* Given an element "q" of a Ramsey parameter tuple whose L[2] contains only 
   "q" returns the Ramsey number for this tuple. */
ramseytrivk(q) := q$


/* *********************
   * Two-colour graphs *
   *********************
*/

/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[3,q]], otherwise returns []. */
ramseyg2_3_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 3 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[3,q]].*/
ramseyg2_3(q) := 
  if q <= 15 then
    [1,3,6,9,14,18,23,28,36,
     [40,43],[46,51],[52,59],[59,59],[66,78],[73,88]][q]
  else unknown$


/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[4,q]], otherwise returns []. */
ramseyg2_4_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 4 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[4,q]].*/
ramseyg2_4(q) := 
  if q <= 15 then
    [1,4,9,18,25,
     [35,41],[49,61],[56,84],[73,115],[92,149],[97,191],[128,238],[133,291],
     [141,349],[153,417]][q]
  else unknown$


/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[5,q]], otherwise returns []. */
ramseyg2_5_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 5 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[5,q]].*/
ramseyg2_5(q) := 
  if q <= 15 then
    [1,5,14,25,[43,49],
     [58,87],[80,143],[101,216],[125,316],[143,442],[159,inf],[185,848],[209,inf],
     [235,1461],[265,inf]][q]
  else unknown$

/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[6,q]], otherwise returns []. */
ramseyg2_6_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 6 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[6,q]].*/
ramseyg2_6(q) := 
  if q <= 15 then
    [1,6,18,[35,41],[58,87],
     [102,165],[113,298],[127,495],[169,780],[179,1171],[253,inf],[262,2566],[317,inf],
     [0,5033],[401,inf]][q]
  else unknown$

/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[7,q]], otherwise returns []. */
ramseyg2_7_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 7 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[7,q]].*/
ramseyg2_7(q) := 
  if q <= 15 then
    [1,7,23,[49,61],[80,143],
     [113,298],[205,540],[216,1031],[233,1713],[289,2826],[405,4553],[416,6954],[511,10581],
     [0,15263],[0,22116]][q]
  else unknown$


/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[8,q]], otherwise returns []. */
ramseyg2_8_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 8 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[8,q]].*/
ramseyg2_8(q) := 
  if q <= 15 then
    [1,8,28,[56,84],[101,216],
     [127,495],[216,1031],[282,1870],[317,3583],[0,6090],[0,10630],[0,16944],[817,27490],
     [0,41525],[861,63620]][q]
  else unknown$


/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[9,q]], otherwise returns []. */
ramseyg2_9_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 9 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[9,q]].*/
ramseyg2_9(q) := 
  if q <= 14 then
    [1,9,36,[73,115],[125,316],
     [169,780],[233,1713],[317,3583],[565,6588],[580,12677],[0,22325],[0,39025],[0,64871],
     [0,89203]][q]
  else unknown$


/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[10,q]], otherwise returns []. */
ramseyg2_10_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 2 and first(L[2]) = 10 and integerp(second(L[2])) and
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[10,q]].*/
ramseyg2_10(q) := 
  if q <= 15 then
    [1,10,[40,43],[92,149],[143,442],
     [179,1171],[289,2826],[0,6090],[580,12677],[798,23556],unknown,[0,81200],unknown,
     unknown,[1265,inf]][q]
  else unknown$

/* **********
   * Graphs *
   **********
*/

/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [2,[3,3,q]], otherwise returns []. */
ramseyg3_3_3_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 3 and 
     L[1] = 2 and first(L[2]) = 3 and second(L[2]) = 3 and 
     integerp(third(L[2])) and third(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [2,[3,3,q]].*/
ramseyg3_3_3(q) := 
  if q <= 3 then 
    [1,6,17][q] 
  else unknown$


/* ***************
   * Hypergraphs *
   ***************
*/

/* Given a Ramsey parameter tuple L, returns a list with "q" as the only 
   element if L is of the form [3,[4,q]], otherwise returns []. */
ramseyhg3_2_4_a(L) := 
  if length(L) = 2 and listp(L[2]) and length(L[2]) = 2 and 
     L[1] = 3 and first(L[2]) = 4 and integerp(second(L[2])) and 
     second(L[2]) >= 0 then [last(L[2])]
  else []$
/* Returns the Ramsey number for a Ramsey parameter tuple of the form 
   [3,[4,q]].*/
ramseyhg3_2_4(q) := 
  if q = 4 then 13 else unknown$


/* ***********
   * Bounds  *
   ***********
*/

/* Returns the upper bound given the result of a call to "ramsey": */
ramsey_ub(n) := if listp(n) then last(n) else n$
/* Returns the upper bound given the result of a call to "ramsey" safely,
   i.e. if the value is unknown, then infinity is returned (so this can be
   used without checking the return value). */
ramsey_ub_s(n) := block([ret],
  ret : if listp(n) then last(n) else n,
  return(if ret = unknown then inf else ret))$


/* Returns the lower bound given the result of a call to "ramsey": */
ramsey_lb(n) := if listp(n) then first(n) else n$
/* Returns the lower bound given the result of a call to "ramsey" safely,
   i.e. if the value is unknown, then 0 is returned (so this can be
   used without checking the return value). */
ramsey_lb_s(n) := block([ret],
  ret : if listp(n) then first(n) else n,
  return(if ret = unknown then 0 else ret))$

/*!
\endhtmlonly
*/
