/* Oliver Kullmann, 17.5.2010 (Swansea) */
/* Copyright 2010, 2012 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Algebra/Lisp/Groupoids/Semigroups/Order.mac
  \brief Functions related to the order of elements

Use by

oklib_load("OKlib/ComputerAlgebra/Algebra/Lisp/Groupoids/Semigroups/Order.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/HashMaps.mac")$

/* Given an associative composition and an element a, compute the closure
   of {a}, i.e., the subsemigroup generated by a:
*/
closure_element_sgr(compo_, a) := block([p : compo_(a,a), res : {a}],
 while not elementp(p,res) do (
  res : adjoin(p,res),
  p : compo_(p,a)
 ),
 res)$

/* Given an associative composition and an element a, compute the
   order of a:
*/
order_element_sgr(compo_, a) := length(closure_element_sgr(compo_, a))$
/* Remarks:
   1. The set of elements of order 1 of a semigroup S is
      idempotents_grd(S).
   2. In a group, the order of an element a is the smallest n >= 0 such
      that a^n = 1.
*/

/* More efficiently, for compo_ and element a decide whether a has
   order precisely n:
*/
test_order_element_sgr(compo_, a, n) := block(
 [p : compo_(a,a), res : {a}, l : 1],
  while not elementp(p,res) and l <= n do (
   res : adjoin(p,res), l : l+1,
   p : compo_(p,a)
  ),
  is(l=n))$

/* For a semigroup S and a number n, the subset of S of elements of
   order precisely n:
*/
subset_order_sgr(S,n) :=
 subset(first(S), lambda([x], test_order_element_sgr(second(S), x, n)))$


/* Computing the triple [index, period, order] of a: */
ipo_element_sgr(compo_, a) := block(
 [p : compo_(a,a), h : sm2hm({[a,1]}), i : 2, index],
  index : ev_hm(h,p),
  while index = false do (
    set_hm(h,p,i),
    i : i + 1,
    p : compo_(p,a),
    index : ev_hm(h,p)
  ),
  [index, i-index, i-1])$
/* Recall: The period of a is the number of elements in the kernel of a,
   that is, the largest subgroup contained in the subsemigroup generated by a,
   while the index is the smallest exponent i such that a^i is in the kernel.
*/

/* Computing a pair [[index,period,order],kernel], where the kernel is
   given as a list of elements, with the first element the first generating
   element (of the kernel) encountered, and with the following elements the
   powers of this element: */
ipokernel_element_sgr(compo_, a) := block(
 [p : compo_(a,a), h : sm2hm({[a,1]}), i : 2, index],
  index : ev_hm(h,p),
  while index = false do (
    set_hm(h,p,i),
    i : i + 1,
    p : compo_(p,a),
    index : ev_hm(h,p)
  ),
  block([period : i-index, order : i-1],
    [[index, period, order],
     map(first, 
         sort(sublist(hm2osm(h), lambda([p], second(p) >= index)),
              lambda([p1,p2], is(second(p1) < second(p2)))))
    ]))$
/* Recall, that the kernel is a cyclic (sub-)group. Thus the last element
   in the kernel list is its neutral element, which is also the (unique)
   idempotent element in the subsemigroup generated by a.
*/

/*!
\endhtmlonly
*/
